# Maintainer: Ryuta Suzuki <oroppas@gmail.com>

_realname=path
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=16.14.0
pkgrel=1
provides=("python3-path=${pkgver}"
          "python-path.py")
conflicts=("python3-path"
           "python-path.py")
replaces=("python3-path"
          "python-path.py")
pkgdesc="File system based database that uses python pickles (msys)"
url="https://github.com/jaraco/path"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: python-path'
  'pypi: path'
)
license=('spdx:MIT')
depends=("python")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-setuptools-scm"
             "python-wheel")
checkdepends=("python-pytest-runner"
              "python-pytest-flake8")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha512sums=('1b35bf06413d3f5da5fd818b2327458ef92a3bcb7e0d8108e4db113e3819f3723384650f7f9e889862bfcd2bc761f2be46932d11023f5a1dcda6bb4c51074ec0')

prepare() {
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE ${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE
}
