# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-expect
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.1.0
pkgrel=1
pkgdesc="py.test plugin to store test expectations and mark tests based on them (msys)"
arch=('any')
url='https://github.com/gsnedders/pytest-expect'
license=('MIT')
depends=("python"
         "python-pytest"
         "python-u-msgpack")
makedepends=("python-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('8392093123dfc9807c28fee251e1d710aaed0d46ee77c7531528fb0a265eb798')

prepare() {
  cd ${srcdir}
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
