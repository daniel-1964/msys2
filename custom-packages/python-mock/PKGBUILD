# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=mock
pkgbase=python-${_realname}
pkgname=( "python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=5.1.0
pkgrel=1
pkgdesc='Rolling backport of unittest.mock for all Pythons (msys)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: mock'
)
url='https://github.com/testing-cabal/mock'
license=('BSD')
depends=("python"
         "python-six"
         "python-pbr")
makedepends=("python-setuptools"
             "python-funcsigs")
source=("https://pypi.python.org/packages/source/m/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('5e96aad5ccda4718e0a229ed94b2024df75cc2d55575ba5762d31f5767b8767d')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true

  cd "${srcdir}"
  cp -r ${_realname}-${pkgver} python-build
}

package() {
  cd ${srcdir}/python-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.txt "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
