# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=dateutil
_pyname=python-dateutil
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.8.1
pkgrel=1
pkgdesc="Provides powerful extensions to the standard datetime module (msys)"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
depends=('python-six')
makedepends=('python-setuptools'
             'python-setuptools-scm')
checkdepends=('python-pytest'
              'python-freezegun'
              'python-hypothesis')
source=("${_realname}-${pkgver}.tar.gz"::"https://files.pythonhosted.org/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=("6B49ACBADCF6BD1CA20667ABCD54FCE3D964BEFB")
sha256sums=('73ebfe9dbf22e832286dafa60473e4cd239f8592f699aa5adaf10050e6e1823c'
            '73ebfe9dbf22e832286dafa60473e4cd239f8592f699aa5adaf10050e6e1823c')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_pyname}-${pkgver}" "python-build"

}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
      /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
      /usr/bin/pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build"

  /usr/bin/python setup.py install --prefix=/usr \
    --root=${pkgdir} --optimize=1 --skip-build

  local _py3basever=$(/usr/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE
  chmod -R a+r $pkgdir//usr/lib/python${_py3basever}/

  # Remove data files
  #rm ${pkgdir}/usr/lib/python${_py3basever}/site-packages/dateutil/zoneinfo/dateutil-zoneinfo.tar.gz
}
