# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>

pkgname=fortune-mod
pkgver=2.12.0
pkgrel=1
pkgdesc="The Fortune Cookie Program from BSD games"
groups=('games')
arch=('x86_64')
url="https://www.shlomifish.org/open-source/projects/fortune-mod/"
license=('BSD')
depends=('gcc-libs'
         'librecode0')
makedepends=('cmake'
             'librecode-devel'
             'rinutils')
source=("https://github.com/shlomif/fortune-mod/archive/fortune-mod-$pkgver.tar.gz"
        "nosymlinks.patch")
sha512sums=('e3feca02b33bcae3c7113432af5d200d7c8701338c1d778c5dd893e4541969146185393c952f19f1b2f17077c4f171cdacbd9b6ba3cb0581117b8aa569cc652f'
            '91800f813eb030e0e55ec7064a49546f0b58f0f27e0524c34f841d61af0c68257a77b6c42074661c9f0c13bd408ad274b0057c4933920f05ca9b329941075495')

prepare() {
  cd "$srcdir/fortune-mod-fortune-mod-$pkgver/fortune-mod"
  # probably a better way to do this but it is baked in hard
  sed -i 's|/games/|/|' CMakeLists.txt
  sed -i 's|"sbin"|"bin"|' CMakeLists.txt
  sed -i 's|"games"|"bin"|' CMakeLists.txt
  sed -i 's|/games/fortunes|/fortune|' datfiles/{,off/}CMakeLists.txt
  patch -Np2 -i "${srcdir}/nosymlinks.patch"
}

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake  "${srcdir}/fortune-mod-fortune-mod-$pkgver/fortune-mod" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DNO_OFFENSIVE=OFF \
        -DLOCALDIR=/usr/share/fortune/ \
        -DLOCALODIR=/usr/share/fortune/off/ \
        -DCOOKIEDIR=/usr/share/fortune/ \
        -DOCOOKIEDIR=/usr/share/fortune/off/
  make REGEXDEFS='-DHAVE_REGEX_H -DPOSIX_REGEX -DHAVE_STDBOOL'
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="$pkgdir" \
       FORTDIR="$pkgdir/usr/bin" \
       COOKIEDIR="$pkgdir/usr/share/fortune" \
       BINDIR="$pkgdir/usr/bin" \
       BINMANDIR="$pkgdir/usr/share/man/man1" \
       FORTMANDIR="$pkgdir/usr/share/man/man6" install
  #rm "$pkgdir/usr/share/fortune/"{,off/}*.u8
  install -Dm644 "${srcdir}/fortune-mod-fortune-mod-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
