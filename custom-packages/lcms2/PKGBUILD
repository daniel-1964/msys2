# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('lcms2' 'liblcms2_2' 'liblcms2-devel')
pkgbase=lcms2
pkgver=2.11
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (msys)"
arch=('any')
url="http://www.littlecms.com"
license=("MIT")
makedepends=("gcc"
             "libtiff-devel"
             "pkg-config")
depends=("gcc-libs")
options=('staticlibs' 'strip')
source=("https://downloads.sourceforge.net/sourceforge/lcms/lcms2-${pkgver}.tar.gz")
sha256sums=('dc49b9c8e4d7cdff376040571a722902b682a795bf92985a85b48854c270772e')

prepare() {
  cd ${pkgbase}-$pkgver
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build" ]] && rm -rf "${srcdir}/build"
  cp -a ${srcdir}/${pkgbase}-${pkgver} "${srcdir}/build"
  cd "${srcdir}/build"
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
  cd "${srcdir}/build"
  make DESTDIR="${srcdir}/dest" install
}

package_lcms2() {
  depends=("liblcms2_2=${pkgver}" "libtiff5")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_liblcms2_2() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
}

package_liblcms2-devel() {
  pkgdesc="Liblcms2 headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("liblcms2_2=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
