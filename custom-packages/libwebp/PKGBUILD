# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Andrew Sun <adsun701@gmail.com>

pkgname=('webp' 'libwebp7' 'libwebpmux3' 'libwebpdemux2' 'libwebpdecoder3' 'libwebp-devel')
pkgbase=libwebp
pkgver=1.1.0
pkgrel=1
pkgdesc="the WebP library. Includes conversion tools (msys)"
arch=('any')
url="https://developers.google.com/speed/webp/"
license=('BSD')
depends=('gcc-libs'
         'libgif7'
         'libjpeg8'
         'libpng16'
         'libtiff5')
makedepends=('gcc'
             'libgif-devel'
             'libjpeg-devel'
             'libpng-devel'
             'libtiff-devel')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/webmproject/libwebp/archive/v${pkgver}.tar.gz)
sha256sums=('424faab60a14cb92c2a062733b6977b4cc1e875a6398887c5911b3a1a6c56c51')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build ]] && rm -rf ${srcdir}/build
  mkdir -p ${srcdir}/build && cd ${srcdir}/build
  ../${pkgbase}-${pkgver}/configure \
    --prefix=/usr \
    --enable-static \
    --enable-swap-16bit-csp \
    --enable-libwebpmux \
    --enable-libwebpdemux \
    --enable-libwebpdecoder \
    --enable-libwebpextras
  make

  cd "${srcdir}/build"
  make install DESTDIR="${srcdir}/dest"
}

package_webp() {
  depends=('libgif7'
           'libjpeg8'
           'libpng16'
           'libtiff5'
           "libwebp7=${pkgver}"
           "libwebpdemux2=${pkgver}"
           "libwebpmux3=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libwebp7() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-webp-7.dll ${pkgdir}/usr/bin/
}

package_libwebpdecoder3() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-webpdecoder-3.dll ${pkgdir}/usr/bin/
}

package_libwebpdemux2() {
  groups=('libraries')
  depends=('gcc-libs'
           "libwebp7=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-webpdemux-2.dll ${pkgdir}/usr/bin/
}

package_libwebpmux3() {
  groups=('libraries')
  depends=('gcc-libs'
           "libwebp7=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-webpmux-3.dll ${pkgdir}/usr/bin/
}

package_libwebp-devel() {
  pkgdesc="Libwebp headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libwebp7=${pkgver}"
           "libwebpdecoder3=${pkgver}"
           "libwebpdemux2=${pkgver}"
           "libwebpmux3=${pkgver}"
           "pkg-config")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
