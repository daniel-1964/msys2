# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=docutils
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=0.16
pkgrel=1
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX (msys)"
arch=('any')
license=('custom')
url="https://docutils.sourceforge.io/"
depends=('python')
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('7d4e999cca74a52611773a42912088078363a30912e8822f7a3d38043b767573')
noextract=(${pkgbase}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${pkgbase}-${pkgver} ]] && rm -rf ${srcdir}/${pkgbase}-${pkgver}
  tar -xzf ${srcdir}/${pkgbase}-${pkgver}.tar.gz -C ${srcdir} || true
  rm -rf python-build | true
  cp -a "${pkgbase}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build --build-lib=build/python
}

check() {
  export LANG=en_US.UTF-8
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  PYTHONPATH="$PWD/build/python/" /usr/bin/python test/alltests.py
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build --build-lib=build/python \
  install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # symlink without .py
  for f in "${pkgdir}/usr"/bin/*.py; do
    ln -s "$(basename $f)" "${pkgdir}/usr/bin/$(basename $f .py)"
    # fix so the file is a script that can run with no issues on the command-line w/o
    # specifying an interpreter
  done
  # setup license
  install -D -m644 COPYING.txt "${pkgdir}/usr/share/licenses/python-${pkgbase}/COPYING.txt"
  install -D -m644 licenses/python* "${pkgdir}/usr/share/licenses/python-${pkgbase}/"
}
