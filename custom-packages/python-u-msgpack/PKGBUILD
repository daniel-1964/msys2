# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=u-msgpack
pkgbase=python-${_realname}
_pkgname=${_realname}-python
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.6.0
pkgrel=1
pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python (msys)"
arch=('any')
url='https://github.com/vsergeev/${_pkgname}'
license=('MIT')
depends=("python")
makedepends=("python-setuptools")
checkdepends=("python-pytest")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/vsergeev/u-msgpack-python/archive/v${pkgver}.tar.gz")
sha256sums=('2b1a6af7992a89f16ba9000ad1483d7921086d2ebe473f5a04576cbc5e9ba70d')

prepare() {
  cd ${srcdir}
  rm -rf python-build | true
  cp -a "${_pkgname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
