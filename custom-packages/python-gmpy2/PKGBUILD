# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=gmpy2
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.2.0
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://github.com/aleaxit/gmpy'
msys2_references=(
  'pypi: gmpy2'
)
license=('LGPL2.1')
depends=("python"
         "mpc")
makedepends=("python-setuptools"
             "mpc-devel")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/aleaxit/gmpy/archive/${_realname}_${pkgver//./_}.tar.gz")
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('ec6817d1392a345ce920e152da077fc7dc5dc19919cd4e0be511c9170d36190f')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  # bsd tar does not handle the symlinks in the distribution file
  # at least if you don't have admin privilleges. This is just a workaround
  # for that.
  tar zxf "${_realname}-${pkgver}.tar.gz" || true

  rm -rf python-build-${MSYSTEM} | true
  cp -a "gmpy-${_realname}_${pkgver//./_}" "python-build-${MSYSTEM}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -d -m 755 ${pkgdir}${MSYSTEM_PREFIX}/share/doc/python-${_realname}
  install -m 644 -t ${pkgdir}${MSYSTEM_PREFIX}/share/doc/python-${_realname} docs/*
}
