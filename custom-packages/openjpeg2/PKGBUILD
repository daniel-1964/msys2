# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=openjpeg
pkgname=('openjpeg2' 'libopenjp2_7' 'libopenjp2-devel' 'libopenjp2-doc')
pkgver=2.5.0
pkgrel=1
pkgdesc="An open source JPEG 2000 codec (msys)"
arch=('i686' 'x86_64')
url="https://www.openjpeg.org/"
license=("BSD")
depends=('gcc-libs'
         'liblcms2_2'
         'libtiff5'
         'libpng16'
         'zlib')
makedepends=('gcc'
             'pkg-config'
             'cmake'
             'liblcms2-devel'
             'libtiff-devel'
             'libpng-devel'
             'zlib-devel')
options=('staticlibs' 'strip')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/uclouvain/openjpeg/archive/v${pkgver}.tar.gz
        0001-fix-install-for-dlls.all.patch
        0002-cmake-files-destination.patch
        0003-versioned-dlls.msys.patch
        0004-msys-disable-java.patch
        0005-sock-jpip.all.patch
        0006-openjpeg-2.1.0-stdcall-for-all-win.patch
        0007-pkgconfig-add-Cflags-private.patch)

sha256sums=('0333806d6adecc6f7a91243b2b839ff4d2053823634d4f6ed7a59bc87409122a'                                                                                  
            '3f3bde353ca3432f157258164c5e3c345af82ca3a9d5a68f815c3030b01cbc32'                                                                                  
            '31b2cc6824b07b705ba1c05b38904c8c4fc6ba39acefebda3e6ec85dbef9a174'                                                                                  
            'd3bf3883b129cf055144ab378e40915968de8336eb2b450ccc5272beb341f2f5'                                                                                  
            'a7426b2699770b128c515de96bd9e8ad7391f01e9c6ec092d019686d9ec7ebac'                                                                                  
            '9df169f03e6e69cba0d7d33592f9eba373a02cd9554b456d3eb9ff3d9135391b'                                                                                  
            '44679801b740ddb75edd4b55031c5f70a02eaf4f8e5eba5f3ad1b397b2ec4aed'                                                                                  
            '997dedd8211fc5e666b078b69ed622dd8ab4ceb05694b3ca593e92a32c151e2e')                                                                                 

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #
prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  apply_patch_with_msg \
   0002-cmake-files-destination.patch \
   0003-versioned-dlls.msys.patch \
   0004-msys-disable-java.patch \
   0007-pkgconfig-add-Cflags-private.patch
  #patch -Nbp1 -i ${srcdir}/0001-fix-install-for-dlls.all.patch 
  #patch -Nbp1 -i ${srcdir}/0005-sock-jpip.all.patch \
  #patch -Nbp1 -i ${srcdir}/0006-openjpeg-2.1.0-stdcall-for-all-win.patch
}

build() {
  msg "Build static version"
  [[ -d ${srcdir}/build-static ]] && rm -rf ${srcdir}/build-static
  mkdir ${srcdir}/build-static
  cd ${srcdir}/build-static
  cmake \
    -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DBUILD_TESTING=OFF \
    -DCMAKE_SYSTEM_PREFIX_PATH=/usr \
    -DBUILD_MJ2=ON \
    -DBUILD_JPWL=ON \
    -DBUILD_JPIP=OFF \
    -DBUILD_JP3D=ON \
    -DBUILD_JAVA=OFF \
    ../${pkgbase}-${pkgver}
  make

  msg "Build shared version"
  [[ -d ${srcdir}/build-shared ]] && rm -rf ${srcdir}/build-shared
  mkdir ${srcdir}/build-shared
  cd ${srcdir}/build-shared
  cmake \
    -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_SYSTEM_PREFIX_PATH=/usr \
    -DBUILD_MJ2=ON \
    -DBUILD_JPWL=ON \
    -DBUILD_JPIP=ON \
    -DBUILD_JP3D=ON \
    -DBUILD_JAVA=OFF \
    -DBUILD_DOC=ON \
    -DBUILD_PKGCONFIG_FILES=ON \
    ../${pkgbase}-${pkgver}
  make

  msg "Install to dest"
  cd "${srcdir}/build-static"
  make DESTDIR=${srcdir}/dest install
  cd "${srcdir}/build-shared"
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd "${srcdir}/build-shared"
  make test || true
}

package_openjpeg2() {
  depends=("libopenjp2_7=${pkgver}")
  mkdir -p ${pkgdir}/usr/{bin,share/man/man1}
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  install -Dpm64 ${srcdir}/dest/usr/share/doc/openjpeg-2.5/LICENSE -t ${pkgdir}/usr/share/licenses/openjpeg-2.5/
  cp -a ${srcdir}/dest/usr/share/man/man1 ${pkgdir}/usr/share/man/
}

package_libopenjp2_7() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
}

package_libopenjp2-devel() {
  pkgdesc="Libopenjpeg2 headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libopenjp2_7=${pkgver}" 'pkg-config')
  mkdir -p ${pkgdir}/usr/{include,lib,share/man/man3}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/share/man/man3 ${pkgdir}/usr/share/man/
}

package_libopenjp2-doc() {
  pkgdesc="Libopenjpeg2 documentation"
  groups=('development')
  mkdir -p ${pkgdir}/usr/share/doc/openjpeg-2.5
  cp -a ${srcdir}/dest/usr/share/doc/openjpeg-2.5/html  ${pkgdir}/usr/share/doc/openjpeg-2.5/
}
