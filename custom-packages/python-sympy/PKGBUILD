# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=sympy
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=1.5.1
pkgrel=1
pkgdesc="Symbolic manipulation package (Computer Algebra System), written in pure Python (msys)"
arch=('any')
url='https://www.sympy.org/en/index.html'
license=('BSD')
depends=("python"
         "python-mpmath")
makedepends=("python-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/sympy/sympy/archive/sympy-${pkgver}.tar.gz")
sha512sums=('3c4220243d2cbcff9eedb2a7986431b5b4d7d0007c78208a60b21830c9a3528d2171086874c42205c1227f779e44c19b9562943e2935d329bc352e7ea85396c7')

prepare() {
  [[ -d python-build ]] && rm -rf python-build
  cp -a "${pkgbase}-${pkgbase}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py test || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build"

  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${pkgbase}/LICENSE"
}
