# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libjpeg-turbo
pkgbase=${_realname}
pkgname=('jpeg' 'libjpeg8' 'libjpeg-devel' 'libturbojpeg0' 'libturbojpeg-devel')
pkgver=3.0.3
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression (msys)"
arch=('i686' 'x86_64')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://libjpeg-turbo.virtualgl.org/"
msys2_repository_url="https://github.com/libjpeg-turbo/libjpeg-turbo"
msys2_pgp_keys_url="https://libjpeg-turbo.org/Downloads/DigitalSignatures"
msys2_references=(
  "cpe: cpe:/a:d.r.commander:libjpeg-turbo"
  "cpe: cpe:/a:libjpeg-turbo:libjpeg-turbo"
)
license=(custom:'BSD-like')
depends=('gcc-libs')
makedepends=('cmake'
             'gcc'
             'ninja'
             'nasm')
options=('staticlibs' 'strip')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/libjpeg-turbo/libjpeg-turbo/archive/${pkgver}.tar.gz
        "0001-header-compat.mingw.patch"
        "libjpeg-turbo-1.3.1-libmng-compatibility.patch")
sha256sums=('a649205a90e39a548863a3614a9576a3fb4465f8e8e66d54999f127957c25b21'
            '166264f617734b33ac55ec8b70e6636d4e409c0d837667afe158e9d28200e6dd'
            '16336caddc949a8a082f39c218b3289288d144bb3b87f62565ed1b294ff8e526')

prepare() {
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i ${srcdir}/0001-header-compat.mingw.patch
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i ${srcdir}/libjpeg-turbo-1.3.1-libmng-compatibility.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MSYSTEM_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MSYSTEM_PREFIX} \
    ${_extra_config[@]} \
    -DWITH_JPEG8=ON \
    -DWITH_SIMD=ON \
    -Denable_SHARED=ON \
    -DENABLE_STATIC=ON \
    ../${_realname}-${pkgver}

  ${MSYSTEM_PREFIX}/bin/cmake.exe --build .

  DESTDIR="${srcdir}/dest" ${MSYSTEM_PREFIX}/bin/cmake.exe --install .

  cp -a "${srcdir}"/${_realname}-${pkgver}/{jinclude,transupp}.h ${srcdir}/dest/${MSYSTEM_PREFIX}/include/

  # Licenses
  # See https://www.libjpeg-turbo.org/About/License
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 README.ijg            "${srcdir}/dest/${MSYSTEM_PREFIX}/share/licenses/${_realname}/README.ijg"
  install -Dm644 README.md  "${srcdir}/dest/${MSYSTEM_PREFIX}/share/licenses/${_realname}/README.md"
  install -Dm644 simd/nasm/jsimdext.inc "${srcdir}/dest/${MSYSTEM_PREFIX}/share/licenses/${_realname}/simd/jsimdext.inc"
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/ctest || true
}

package_jpeg() {
  depends=("libjpeg8=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*jp{,e}g*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libjpeg8() {
  provides=('libjpeg')
  conflicts=('libjpeg')
  replaces=('libjpeg')
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-jpeg-8.dll ${pkgdir}/usr/bin/
}

package_libjpeg-devel() {
  pkgdesc="Libjpeg headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libjpeg8=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib/pkgconfig}
  cp -a ${srcdir}/dest/usr/include/{j*,transupp}.h ${pkgdir}/usr/include
  cp -a ${srcdir}/dest/usr/lib/libjpeg.* ${pkgdir}/usr/lib
  cp -a ${srcdir}/dest/usr/lib/pkgconfig/libjpeg.pc ${pkgdir}/usr/lib/pkgconfig
}

package_libturbojpeg0() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/{msys-turbojpeg-0.dll,tjbench.exe} ${pkgdir}/usr/bin/
}

package_libturbojpeg-devel() {
  pkgdesc="Libturbojpeg headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libturbojpeg0=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib/pkgconfig}
  cp -a ${srcdir}/dest/usr/include/turbojpeg.h ${pkgdir}/usr/include
  cp -a ${srcdir}/dest/usr/lib/libturbojpeg.* ${pkgdir}/usr/lib
  cp -a ${srcdir}/dest/usr/lib/pkgconfig/libturbojpeg.pc ${pkgdir}/usr/lib/pkgconfig
  cp -a ${srcdir}/dest/usr/lib/cmake ${pkgdir}/usr/lib/
}
