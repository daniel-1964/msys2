# Maintainer: Andrew Sun <adsun701@gmail.com>

_realname=tox
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=3.20.0
pkgrel=1
pkgdesc="Python virtualenv management and testing tool (msys)"
arch=('any')
url="https://tox.readthedocs.io"
license=('GPL2')
depends=("python"
         "python-py"
         "python-six"
         "python-virtualenv"
         "python-setuptools"
         "python-setuptools-scm"
         "python-filelock"
         "python-toml"
         "python-pluggy")
makedepends=("util-linux")
checkdepends=('python-pathlib2'
              'python-freezegun')
#install=${_realname}3.install
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://pypi.io/packages/source/t/tox/${_realname}-${pkgver}.tar.gz")
sha256sums=('eb629ddc60e8542fd4a1956b2462e3b8771d49f1ff630cecceacaa0fbfb7605a')

prepare() {
  find ${_realname}-${pkgver} -name "*.pyc" -delete
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  msg "Python build for msys"
  /usr/bin/virtualenv "$srcdir/pyvenv" --system-site-packages
  . "$srcdir/pyvenv/bin/activate"
  /usr/bin/python setup.py install
  /usr/bin/python setup.py pytest
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"

  #local PREFIX_WIN=$(cygpath -wm /usr)
  ## fix python command in files
  #for _f in "${pkgdir}/usr"/bin/*.py; do
  #  sed -e "s|${PREFIX_WIN}/bin/|/usr/bin/|g" -i ${_f}
  #done
}
