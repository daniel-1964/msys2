# Maintainer: Frederic Wang <fred.wang@free.fr>

_realname=idna
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=3.1
pkgrel=1
pkgdesc='Internationalized Domain Names in Applications (msys)'
url='https://github.com/kjd/idna'
license=('BSD-like')
arch=('any')
depends=("python")
makedepends=("python-setuptools")
_dtoken="d8/82/28a51052215014efc07feac7330ed758702fc0581347098a81699b5281cb"
source=("https://pypi.io/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('c5b02147e01ea9920e6b0a3f1f7bb833612d507592c837a6c49552768f4054e1')

prepare() {
  cd ${srcdir}
  rm -rf python-build
  cp -a ${_realname}-${pkgver} python-build
}

build() {
  msg "Python build for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for Msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py test
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}"  --optimize=1 --skip-build
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
