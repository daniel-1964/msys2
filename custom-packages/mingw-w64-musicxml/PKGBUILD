# Maintainer: Michael Gogins <michael.gogins@gmail.com>

_realname=musicxml
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgrel=1
pkgver=3.22
pkgdesc="library and tools for the MusicXML format (git version)"
arch=('x86_64' 'i686')
url="https://github.com/dfober/libmusicxml"
license=('LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
provides=("${MINGW_PACKAGE_PREFIX}-libmusicxml")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=(!libtool strip staticlibs)
conflicts=("${MINGW_PACKAGE_PREFIX}-libmusicxml")
source=("$_realname-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "$_realname.patch")
sha256sums=('083e87f33ee504f95ca637cc24b19d2d650abc97984778ff9eeb03194f30c97c'
            '3db36bac862aa26255dff59bc33c0ee57f2f39e87bed11952050d2e95f07e9a1')

prepare() {
    cd ${srcdir}/lib${_realname}-${pkgver}
    patch -p1 -i "${srcdir}/$_realname.patch"
}

build() {
    [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
    mkdir -p "${srcdir}/build-${MINGW_CHOST}"
    cd "${srcdir}/build-${MINGW_CHOST}"
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
        ${MINGW_PREFIX}/bin/cmake \
            -GNinja \
            -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
            ../lib${_realname}-${pkgver}/build

    ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
    cd "${srcdir}/build-${MINGW_CHOST}"

    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --build . --target install
    cd ${pkgdir}/${MINGW_PREFIX}/share/doc/$_realname
    mv doc/* .
    rmdir doc
    mv ../../../README.html ../../../CHANGELOG.txt .
    find ${srcdir}/lib${_realname}-${pkgver}/doc -name '*.pdf' -exec cp -a --target ${PWD} {} +
    #disabled cp -a ${srcdir}/lib${_realname}-${pkgver}/files/samples .
}
