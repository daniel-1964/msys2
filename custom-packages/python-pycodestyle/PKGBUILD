# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
#This value is here because some python package names have capital letters
#while there is a strong preference for lowercase letters in ArchLinux and
#MSYS packages.

_realname=pycodestyle
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}"
          "pep8-python3"
          "python3-pep8")
pkgver=2.5.0
pkgrel=1
pkgdesc="Python style guide checker (msys)"
arch=('any')
url='https://github.com/PyCQA/pycodestyle'
license=('custom:Expat')
depends=('python')
makedepends=('python3-setuptools')
#install=${_realname}3.install
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/PyCQA/pycodestyle/archive/${pkgver}.tar.gz")
sha256sums=('a603453c07e8d8e15a43cf062aa7174741b74b4a27b110f9ad03d74d519173b5')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  # two of these tests are known to fail in Windows
  /usr/bin/python setup.py test || true
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"

  #local PREFIX_WIN=$(cygpath -wm /usr)
  ## fix python command in files
  #for _f in "${pkgdir}/usr"/bin/*.py; do
  #  sed -e "s|${PREFIX_WIN}|/usr|g" -i ${_f}
  #done
}
