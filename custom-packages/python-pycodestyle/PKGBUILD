# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pycodestyle
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=2.12.0
pkgrel=2
pkgdesc="Python style guide checker (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: python-pycodestyle'
  'pypi: pycodestyle'
)
msys2_repository_url='https://github.com/pycqa/pycodestyle/'
url='https://pycodestyle.pycqa.org/'
license=('custom:Expat')
depends=("python")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-wheel")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('442f950141b4f43df752dd303511ffded3a04c2b6fb7f65980574f0c31e6e79c')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m unittest discover testsuite || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
