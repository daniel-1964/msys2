# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=youtube-dl
pkgbase=${_realname}
pkgname=("${_realname}")
pkgver=2020.11.12
pkgrel=1
pkgdesc="A command-line program to download videos from YouTube.com and a few more sites (msys)"
arch=('any')
url="https://youtube-dl.org"
license=('custom')
depends=("python"
         "python-setuptools")
#optdepends=('ffmpeg: for video post-processing'
            #'rtmpdump: for rtmp streams support'
            #'atomicparsley: for embedding thumbnails into m4a files'
            #'python-pycryptodome: for hlsnative downloader')
source=("${url}/downloads/${pkgver}/${_realname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('aeb00b2079c4711de7eea2a91f6923ebade84c5e985b0caeb47d572395d42831'
            'SKIP')
validpgpkeys=('ED7F5BF46B3BBED81C87368E2C393E0F18A9236D') # Sergey M. <dstftw@gmail.com>

build() {
  cd "${srcdir}/${_realname}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}"

  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
}
