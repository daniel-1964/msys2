# Maintainer: Eric BÃ©langer <eric@archlinux.org>

_realname=vorbis-tools
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.0
pkgrel=1
pkgdesc='Extra tools for Ogg-Vorbis'
arch=('any')
url='https://www.xiph.org/vorbis/'
license=('GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-libao"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-speex"
         "${MINGW_PACKAGE_PREFIX}-flac"
         "${MINGW_PACKAGE_PREFIX}-opusfile"
         "${MINGW_PACKAGE_PREFIX}-libkate")
source=("https://downloads.xiph.org/releases/vorbis/${_realname}-${pkgver}.tar.gz"
        '0001-vorbis-tools-cve9638-cve9639.patch'
        '0002-vorbis-tools-cve9640.patch'
        '0003-vorbis-tools-CVE-2015-6749.patch'
        '0004-fix-segfault-in-vcut.patch'
        '0005-ogg123-mingw.patch'
        '0006-opus-support.patch')
sha256sums=('a389395baa43f8e5a796c99daf62397e435a7e73531c9f44d9084055a05d22bc'
            '5a6d74de56e67613c4887e3fe9102aadfce4cd4a9a477fb2c1ed1a26f11fdff6'
            'ac4dfaba96c1f324c79dd81c88386e62508114f24d1cadcce9be48a87e58980a'
            '52f1d083c16b9caa2bc7bd5315210bac810dd6a0565e4c270650177a4c815fa7'
            '747984e0091e5ed68c190e502f1e9b6871cda216f85f3e52879986a48973938b'
            'f9c1d349da24568f9937e578049b262ee2d7e59924d13f2134c91d376383a015'
            '7e044cc8d6136f11cfe9f15dab14e7085f3bd486a6de198e7a99936675b4962f')

prepare() {
  rm -f "${srcdir}/${_realname}-${pkgver}/ogg123/opus_format.c"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0001-vorbis-tools-cve9638-cve9639.patch"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0002-vorbis-tools-cve9640.patch"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0003-vorbis-tools-CVE-2015-6749.patch"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0004-fix-segfault-in-vcut.patch"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0005-ogg123-mingw.patch"
  patch -d "${srcdir}/${_realname}-${pkgver}" -Np1 -i "${srcdir}/0006-opus-support.patch"
  cd "${srcdir}/${_realname}-${pkgver}"
  autoreconf -fiv
}

build() {
  mkdir -p $srcdir/build-${MINGW_CHOST} && cd $srcdir/build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --with-flac \
    --with-kate \
    --with-opus \
    --with-speex \
    --enable-vcut
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="${pkgdir}" install
}
