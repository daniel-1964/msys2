# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-runner
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=6.0.1
pkgrel=1
provides=("python3-${_realname}=${pkgver}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgdesc="Invoke py.test as distutils command with dependency resolution (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/pytest-dev/pytest-runner'
msys2_references=(
  'pypi: pytest-runner'
)
license=('MIT')
depends=("python")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-setuptools-scm"
             "python-wheel")
checkdepends=("python-pytest-black"
              "python-pytest-cov"
              "python-pytest-enabler"
              "python-pytest-flake8"
              "python-pytest-virtualenv")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/pytest-dev/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('edbe5b53590ae1e5cb598b6f678e31a662aea97ea0bee92188e4d9fd26b71b71')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}


build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MSYSTEM_PREFIX}/bin/python -m pytest
}

package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
