# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_pyname=pytest-virtualenv
_realname=pytest-virtualenv
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=1.7.0
pkgrel=5
pkgdesc='Virtualenv fixture for py.test (msys)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: pytest-virtualenv'
)
url="https://github.com/manahl/pytest-plugins"
license=('MIT')
depends=(
  "python-pytest-fixture-config"
  "python-pytest-shutil"
  "python-pytest"
  "python-virtualenv"
)
makedepends=(
  "python-setuptools"
  "python-setuptools-scm"
  "python-setuptools-git"
)
source=("${_pyname}-${pkgver}.tar.gz::https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('2270ee8822111ec25db48e9d9f2efec32e68483a015b14cd0d92aeccc6ff820f')

prepare() {  
  cd "$srcdir"
  rm -rf python-build | true
  cp -r "${_pyname//_/-}-$pkgver" "python-build"
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
