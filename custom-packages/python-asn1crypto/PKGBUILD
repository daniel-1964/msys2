# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=asn1crypto
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=1.3.0
pkgrel=1
pkgdesc="Python ASN.1 library with a focus on performance and a pythonic API (msys)"
url='https://github.com/wbond/asn1crypto'
license=('MIT')
arch=('any')
depends=('python-pycparser')
makedepends=('python-setuptools')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/wbond/asn1crypto/archive/${pkgver}.tar.gz")
sha256sums=('09788af39fee5a0eca6390a85fc79acbd946b8a0938442f4a2f0634e27d64075')

prepare() {
  cd ${srcdir}
  cp -a ${pkgbase}-${pkgver} python-build
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd ${srcdir}/python-build
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr"/share/licenses/python-${pkgbase}/LICENSE
}
