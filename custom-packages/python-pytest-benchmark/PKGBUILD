# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>.

_realname=pytest-benchmark
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=4.0.0
pkgrel=2
pkgdesc="A py.test fixture for benchmarking code (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/ionelmc/pytest-benchmark'
msys2_references=(
  'pypi: pytest-benchmark'
)
license=('BSD')
depends=("python"
         "python-py-cpuinfo"
         "python-pytest")
makedepends=("python-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/ionelmc/pytest-benchmark/archive/v${pkgver}.tar.gz")
sha256sums=('42ae209b04764022a286524f2ed32deb35432f997430b067105896f8a990c5f1')

prepare() {
  rm -rf python-build | true
  cp -r "${_realname}-${pkgver}" "python-build"

  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
