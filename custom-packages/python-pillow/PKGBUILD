# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Pillow
pkgbase=python-pillow
pkgname=("python-pillow")
pkgver=7.2.0
pkgrel=1
provides=("python3-${_realname}=${pkgver}"
          "python3-pillow=${pkgver}")
conflicts=("python3-${_realname}"
           "python3-pillow")
replaces=("python3-${_realname}"
          "python3-pillow")
arch=('any')
license=('custom')
url="https://github.com/python-pillow/Pillow/"
pkgdesc="Python Imaging Library (PIL) fork Python3 version (msys)"
depends=('libfreetype6'
         'liblcms2_2'
         'libjpeg8'
         'libtiff5'
         'libwebp7'
         'libwebpdemux2'
         'libwebpmux3'
         'libimagequant'
         'libopenjp2_7'
         'python'
         'python-olefile'
         'zlib')
makedepends=('python-setuptools'
             'libfreetype-devel'
             'python-devel'
             'libcrypt-devel'
             'liblcms2-devel'
             'libjpeg-devel'
             'libtiff-devel'
             'libwebp-devel'
             'libimagequant-devel'
             'libopenjp2-devel'
             'zlib-devel')

options=('staticlibs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/python-pillow/Pillow/archive/${pkgver}.tar.gz
        001-libtiff-fileio.patch
        002-fix-photoimage-crash.patch
        003-remove-hardcoded-include-path.patch)
sha256sums=('0fd19382a1d76d3296f333223ed9429d2110cc0b44ad20b10c5f6255ce9a3bf4'
            'daed469b7ba87ee03830331d79492364966981effe6cf4c71c731a868628adc2'
            '1ba2504ab80b0078fbb92b63775ff9e7ef2e637253d6999ace4c023c112c796c'
            'b5067befed4fc347106084f60e5b553c299739dc82fa13915e4480fc1ae19739')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -Np1 -i "${srcdir}"/001-libtiff-fileio.patch
  patch -Np1 -i "${srcdir}"/002-fix-photoimage-crash.patch
  patch -Np1 -i "${srcdir}"/003-remove-hardcoded-include-path.patch

  cd ${srcdir}
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  cd "${srcdir}"
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
      /usr/bin/python setup.py build
}

check() {
  msg "Python test for ${CARCH}"
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
      /usr/bin/python selftest.py || true

  #setup.py test
}

package() {
  local _mingw_prefix=$(cygpath -am /usr)
  local _py3basever=$(/usr/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    /usr/bin/python setup.py install \
   --prefix=${MINGW_PREFIX#\/} --root="${pkgdir}/" \
   --optimize=0 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
  install -dm755 "${pkgdir}/usr/include/python${_py3basever}m/"
  install -m644 -t "${pkgdir}/usr/include/python${_py3basever}m/" src/libImaging/*.h

  # clean up bins
  #  cd "${pkgdir}/usr/bin"
  #  for f in *.py; do
  #    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i ${f}
  #    mv "$f" "${f%.py}"
  #  done
}
