# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Pillow
pkgbase=python-pillow
pkgname=("python-pillow")
pkgver=8.1.2
pkgrel=1
provides=("python3-${_realname}=${pkgver}"
          "python3-pillow=${pkgver}")
conflicts=("python3-${_realname}"
           "python3-pillow")
replaces=("python3-${_realname}"
          "python3-pillow")
arch=('any')
license=('custom')
url="https://github.com/python-pillow/Pillow/"
pkgdesc="Python Imaging Library (PIL) fork Python3 version (msys)"
depends=('libfreetype6'
         'liblcms2_2'
         'libjpeg8'
         'libtiff5'
         'libwebp7'
         'libwebpdemux2'
         'libwebpmux3'
         'libimagequant'
         'libopenjp2_7'
         'python'
         'python-olefile'
         'zlib')
makedepends=('python-setuptools'
             'libfreetype-devel'
             'python-devel'
             'libcrypt-devel'
             'liblcms2-devel'
             'libjpeg-devel'
             'libtiff-devel'
             'libwebp-devel'
             'libimagequant-devel'
             'libopenjp2-devel'
             'zlib-devel')

options=('staticlibs')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/python-pillow/Pillow/archive/${pkgver}.tar.gz)
sha256sums=('4b99c0a07e8bc4048b4f37ee515d02cc2f895453afe534e4b00bfe2f2a2dbe39')

prepare() {
  cd ${srcdir}
  rm -rf python-build | true
  cp -r "${_realname}-${pkgver}" "python-build"
}

build() {
  cd "${srcdir}"
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for Msys"
  cd "${srcdir}/python-build"
    /usr/bin/python selftest.py || true

  #setup.py test
}

package() {
  local _pybasever=$(/usr/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install \
   --prefix=/usr --root="${pkgdir}/" \
   --optimize=0 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
  install -dm755 "${pkgdir}/usr/include/python${_pybasever}/"
  install -m644 -t "${pkgdir}/usr/include/python${_pybasever}/" src/libImaging/*.h

  # clean up bins
  #  cd "${pkgdir}/usr/bin"
  #  for f in *.py; do
  #    sed -e "s|${_mingw_prefix}/bin/|/usr/bin/env |g" -i ${f}
  #    mv "$f" "${f%.py}"
  #  done
}
