# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=('libfreetype6' 'libfreetype-devel')
pkgbase=freetype
pkgver=2.12.1
pkgrel=1
pkgdesc="TrueType font rendering library (msys)"
arch=('i686' 'x86_64')
url="https://www.freetype.org/"
license=(GPL2+ custom:FreeType)
depends=("gcc-libs"
         "brotli"
         "libbz2"
         "libpng16"
         "zlib")
makedepends=("gcc"
             "cmake"
             "libbz2-devel"
             "brotli-devel"
             "zlib-devel"
             "libpng-devel"
             "pkg-config")
options=('strip' '!libtool' 'staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}-${pkgver}.tar.xz{,.sig}
        freetype-2.10.0-enable-valid.patch
        freetype-2.10.0-enable-spr.patch
        freetype-2.5.1-enable-sph.patch)
sha256sums=('4766f20157cc4cf0cd292f80bf917f92d1c439b243ac3018debf6b9140c41a7f'
            'SKIP'
            '30e84993f899b66c31451992667f7ac6cc9e0b19dfe2f47ccb546d77bbc77ef5'
            '0571f18d97834df020e4cbdb1338b14f9160669f868237080951e01f5b86727c'
            '324012ad68388d22c63368314721d2360b48dd40daa808d0383699a281d931b0')
validpgpkeys=('E30674707856409FF1948010BE6C3AAC63AD8E3F')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  plain "freetype-2.10.0-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.10.0-enable-valid.patch"
  plain "freetype-2.10.0-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.10.0-enable-spr.patch"

  # Disabled for now due to resistance
  # Kept here for easier rebuilds via ABS
  # https://bugs.archlinux.org/task/35274
  #patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-sph.patch"
}

build() {
  msg "Build static version"
  [[ -d ${srcdir}/build-static ]] && rm -rf ${srcdir}/build-static
  mkdir ${srcdir}/build-static && cd ${srcdir}/build-static
  cmake \
    -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SYSTEM_PREFIX_PATH=/usr \
    -DBUILD_SHARED_LIBS=OFF \
    -DBUILD_TESTING=OFF \
    -DFT_WITH_PNG=ON  \
    -DFT_WITH_ZLIB=ON  \
    -DFT_WITH_BZIP2=ON \
    -DFT_WITH_BROTLI=ON \
    -DFT_WITH_HARFBUZZ=OFF \
    ../freetype-${pkgver}
  make

  msg "Build shared version"
  [[ -d "${srcdir}/build-shared" ]] && rm -rf "${srcdir}/build-shared"
  mkdir -p "${srcdir}/build-shared" && cd "${srcdir}/build-shared"
  cmake \
    -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SYSTEM_PREFIX_PATH=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=ON \
    -DFT_WITH_PNG=ON  \
    -DFT_WITH_ZLIB=ON  \
    -DFT_WITH_BZIP2=ON \
    -DFT_WITH_BROTLI=ON \
    -DFT_WITH_HARFBUZZ=OFF \
    ../freetype-${pkgver}
  make

  msg "Install to dest"
  cd "${srcdir}/build-static"
  make DESTDIR=${srcdir}/dest install
  cd "${srcdir}/build-shared"
  make DESTDIR=${srcdir}/dest install
  # Licenses
  install -pDm644 "${srcdir}/${pkgbase}-${pkgver}/docs/GPLv2.TXT"   "${srcdir}/dest/usr/share/licenses/${pkgbase}/GPLv2.TXT"
  install -pDm644 "${srcdir}/${pkgbase}-${pkgver}/docs/FTL.TXT"     "${srcdir}/dest/usr/share/licenses/${pkgbase}/FTL.TXT"
}

check() {
  plain "No tsts are defined"
  #cd "${srcdir}/build-shared"
  #make test
}

package_libfreetype6() {
  groups=('libraries')
  depends=("gcc-libs"
           "brotli"
           "libbz2"
           "libpng16"
           "zlib")
  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libfreetype-devel() {
  pkgdesc="Libfreetype headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libfreetype6=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
