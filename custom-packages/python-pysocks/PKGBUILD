# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_pyname=PySocks
_realname=pysocks
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.7.1
pkgrel=1
pkgdesc="SOCKS4, SOCKS5 or HTTP proxy (Anorov fork PySocks replaces socksipy) (msys)"
arch=('any')
url='https://github.com/Anorov/PySocks'
msys2_references=(
  'archlinux: python-pysocks'
  'pypi: PySocks'
)
license=('spdx:BSD-3-Clause')
depends=("python")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-wheel")
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha512sums=('cef4a5ce8c67fb485644696a23bf68a721db47f3211212de2d4431eaf9ebd26077dd5a06f6dfa7fde2dcb9d7c1ed551facd014e999929cb4d7b504972c464016')

build() {
  cp -r "${_pyname}-${pkgver}" "python-build" && cd "python-build"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
