# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>.

_realname=asgiref
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=3.3.1
pkgrel=1
pkgdesc="Reference ASGI adapters and channel layers (msys)"
arch=('any')
url='http://github.com/django/asgiref'
license=('BSD')
depends=("python")
makedepends=("python"
             "python-setuptools")
#checkdepends=("python-pytest-asyncio" "python-pytest")
options=('staticlibs' 'strip' '!debug')
source=("https://pypi.io/packages/source/a/asgiref/asgiref-${pkgver}.tar.gz")
sha256sums=('7162a3cb30ab0609f1a4c95938fd73e8604f63bdba516a7f7d64b83ff09478f0')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"

  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

# Note that build() is sometimes skipped because it's done in
# the packages setup.py install for simplicity if you can do so.
# but sometimes, you want to do a check before install which would
# also trigger the build.
build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

#check() {
#  msg "Python test for msys"
#  cd "${srcdir}/python-build"
#  PYTHONPATH=. /usr/bin/pytest .
#}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"

}
