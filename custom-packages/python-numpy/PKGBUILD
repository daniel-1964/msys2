# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Duong Pham <dthpham@gmail.com>

_realname=numpy
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.0.0
pkgrel=1
pkgdesc="Scientific tools for Python (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:BSD-3-Clause')
url="https://www.numpy.org/"
msys2_references=(
  "cpe: cpe:/a:numpy:numpy"
  "pypi: numpy"
)
makedepends=("python-build"
             "python-installer"
             "python-wheel"
             "meson-python"
             "pkgconf"
             "ninja"
             "cython"
             "python-devel")
checkdepends=("python-pytest")
depends=("python")
optdepends=("python-pytest: testsuite")
options=(!strip)
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz
        0001-numpy-for-msys2.patch)
sha256sums=('cf5d1c9e6837f8af9f92b6bd3e86d513cdc11f60fd62185cc49ec7d1aba34864'
            '1bec1bc99a781bccb597807223ef08864c742dfc1e7dba3d409b946014c6ac09')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${_realname}-${pkgver}

  apply_patch_with_msg \
    0001-numpy-for-msys2.patch

  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd python-build-${MSYSTEM}

  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation \
    
}

package() {
  cd python-build-${MSYSTEM}
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  find "${pkgdir}${MSYSTEM_PREFIX}" -name '*.dll.a' -delete

  _pyver=$(${MSYSTEM_PREFIX}/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")
  install -m755 -d "${pkgdir}${MSYSTEM_PREFIX}/include/python${_pyver}"
  cp -af ${pkgdir}${MSYSTEM_PREFIX}/lib/python${_pyver}/site-packages/${_realname}/_core/include/${_realname} "${pkgdir}${MSYSTEM_PREFIX}/include/python${_pyver}/"

  install -Dm644 LICENSE.txt ${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt
}
