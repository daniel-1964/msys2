# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=hypothesis
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=6.108.2
pkgrel=2
pkgdesc="Advanced Quickcheck style testing library for Python (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: python-hypothesis'
  'pypi: hypothesis'
)
msys2_changelog_url='https://hypothesis.readthedocs.io/en/latest/changes.html'
msys2_documentation_url='https://hypothesis.readthedocs.io/'
msys2_issue_tracker_url='https://github.com/HypothesisWorks/hypothesis/issues/'
msys2_repository_url='https://github.com/HypothesisWorks/hypothesis/tree/master/hypothesis-python/'
url='https://hypothesis.works/'
license=('spdx:MPL-2.0')
depends=("python-attrs"
         "python-sortedcontainers"
         "python-exceptiongroup")
if [[ ${CARCH} != i686 ]]; then
optdepends=("python-pytz: for datetime and django module"
            "python-faker: for fakefactory and django module"
            "python-django: for django module"
            "python-numpy: for numpy module"
            "python-pytest: for pytest module")
fi
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-wheel")
checkdepends=("python-pytest")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('62cf1c16bd98548b6a84007c5fb8cf6d9cb358dad870adb4f236c795ef162fdd')

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.txt "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt"
}
