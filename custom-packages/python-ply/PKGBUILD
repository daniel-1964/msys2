# Maintainer: Andrew Sun <adsun701@gmail.com>

pkgbase=ply
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=3.11
pkgrel=1
pkgdesc="Implementation of lex and yacc parsing tools (msys)"
arch=('any')
DLAGENTS=("https::/usr/bin/curl -k -o %o %u")
url="https://www.dabeaz.com/ply/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
options=('staticlibs' 'strip' '!debug')
source=("https://www.dabeaz.com/ply/${pkgbase}-${pkgver}.tar.gz"
        "LICENSE")
sha256sums=('00c7c1aaa88358b9c765b6d3000c6eec0ba42abca5351b095321aef446081da3'
            '87c20dd0a774f4d4ff837b4f1555f7eb1ed1b8dc1e3223cd105e5c1e282d62bf')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${pkgbase}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/python-${pkgbase}/LICENSE"
}
