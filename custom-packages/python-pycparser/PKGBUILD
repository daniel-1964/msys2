# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=pycparser
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.22
pkgrel=1
pkgdesc='Complete parser of the C language, written in pure Python (msys)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/eliben/pycparser'
msys2_references=(
  'pypi: pycparser'
)
license=('spdx:BSD-3-Clause')
depends=("python")
makedepends=("python-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/eliben/pycparser/archive/release_v${pkgver}.tar.gz")
sha256sums=('b074c239ee828fcb9c97774b942f3ce51f0d2edc00809f49c3c3ef0f3baaf9c1')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -r "${_realname}-release_v${pkgver}" "python-build"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
  cd pycparser
  ${MSYSTEM_PREFIX}/bin/python _build_tables.py
}

check() {
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python -m unittest discover
}

package() {
  cd "${srcdir}/python-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  
  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
