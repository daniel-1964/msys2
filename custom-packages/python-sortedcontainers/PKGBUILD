# Contributor: Christoph Reiter <reiter.christoph@gmail.com>

_realname=sortedcontainers
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=2.4.0
pkgrel=4
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: sortedcontainers'
)
url="http://www.grantjenks.com/docs/sortedcontainers/"
license=('Apache')
depends=("python")
makedepends=("python-setuptools")
source=(https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('25caa5a06cc30b6b83d11423433f65d1f9d76c4c6a0c90e3379eaa43b9bfdb88')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
        --root "${pkgdir}" --optimize=1 --skip-build

  install -Dm644 "LICENSE" "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
