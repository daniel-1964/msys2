# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=olefile
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgdesc="Python library to parse, read and write Microsoft OLE2 files (formerly OleFileIO_PL) (msys)"
pkgver=0.46
pkgrel=1
arch=('any')
license=('BSD')
url="https://www.decalage.info/olefile"
depends=("python")
makedepends=("python-setuptools")
options=('staticlibs')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/decalage2/olefile/archive/v${pkgver}.tar.gz)
sha256sums=('11d1a3810e50956c9360a4c76f1ecebf3e54f9d8406f75127bd0d296638488f4')

prepare() {
  cd ${srcdir}
  [[ -d python-build ]] && rm -rf python-build
  cp -r "${pkgbase}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python3 setup.py build
}

package() {
  local _py3basever=$(/usr/bin/python3 -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")

  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}/" --optimize=0 --skip-build
  install -Dm644 doc/LICENSE.rst "${pkgdir}/usr/share/licenses/python-${pkgbase}/LICENSE"
  install -Dm644 README.md doc/* -t "${pkgdir}/usr/share/doc/python-${pkgbase}/README"
}
