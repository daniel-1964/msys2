# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: juergen <juergen@archlinux.org>

_realname=ecl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=24.5.10
# Remember to rebuild sagemath when the soname changes
pkgrel=1
pkgdesc="Embeddable Common Lisp"
arch=('any')
url="https://common-lisp.net/project/ecl/"
license=('LGPL')
depends=('bash' 
         "${MINGW_PACKAGE_PREFIX}-gc"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libatomic_ops"
         "${MINGW_PACKAGE_PREFIX}-libffi")
makedepends=('texinfo'
            "${MINGW_PACKAGE_PREFIX}-cc")
provides=("${MINGW_PACKAGE_PREFIX}-common-lisp"
          "${MINGW_PACKAGE_PREFIX}-cl-asdf")
options=('!makeflags')
source=("https://common-lisp.net/project/ecl/static/files/release/${_realname}-${pkgver}.tgz"
      "ecl-mingw.patch")
sha256sums=('e4ea65bb1861e0e495386bfa8bc673bd014e96d3cf9d91e9038f91435cbe622b'
            '232845b91d7a614f2247442a16c6b303c72983e05a6a42b5d66c8a7a7b146ef5')

prepare() {
  cd $_realname-$pkgver
  patch -Np1 -i "${srcdir}/ecl-mingw.patch"
}

build() {
  cd $_realname-$pkgver
  unset ECLDIR || true
  CFLAGS+=" -Wno-array-bounds -Wno-unused-function -Wno-unused-but-set-variable -Wno-unused-variable -fcommon -ffat-lto-objects" \
  ./configure \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-rpath \
    --enable-boehm=system \
    --enable-gmp=system \
    --enable-libatomic=system \
    --enable-manual=info \
    --enable-shared \
    --enable-threads \
    --with-bytecmp \
    --with-clos-streams \
    --with-cmp \
    --with-defsystem \
    --with-rt \
    --with-serve-event \
    --with-sse \
    --with-tcp \
    --without-cxx \
    --without-x \
    --with-libffi-prefix=${MINGW_PREFIX}

  make
}

package() {
  make -C $_realname-$pkgver DESTDIR="$pkgdir" install
  # Make install for mingw is ugly: we correct it
  cd ${pkgdir}${MINGW_PREFIX}
  mkdir -p share/licenses/${_realname}
  mv COPYING* LICENSE* share/licenses/${_realname}
  rm -fr ecl/gc
  mkdir include
  mv ecl include/
  mkdir -p lib/ecl
  mv *.a *.fas *.asd encodings help.doc lib/ecl
  rm -f TAGS build-stamp
  mkdir bin
  mv *.exe *.dll ecl-config bin
  mkdir -p etc/profile.d
  cat >etc/profile.d/ecl.sh <<EOF
export ECLDIR=$(cygpath -m ${MINGW_PREFIX}/lib/ecl/)
EOF
}
