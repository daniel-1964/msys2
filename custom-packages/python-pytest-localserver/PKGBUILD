# Maintainer: Sarah Ottinger

_realname=pytest-localserver
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=0.5.0
pkgrel=1
pkgdesc='py.test plugin to test server connections locally (msys)'
arch=('any')
url="https://bitbucket.org/pytest-dev/pytest-localserver"
license=('MIT')
depends=("python-pytest" "python-werkzeug")
makedepends=("python-setuptools")
checkdepends=("python-requests")
source=("https://files.pythonhosted.org/packages/source/${_realname:0:1}/$_realname/$_realname-$pkgver.tar.gz")
sha256sums=('3a5427909d1dfda10772c1bae4b9803679c0a8f04adb66c338ac607773bfefc2')

prepare() {
  cd "$srcdir"
  rm -rf python-build | true
  cp -a "${_realname}-$pkgver" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="$PWD/tmp_install" --optimize=1 --skip-build
  PYTHONPATH="$PWD/tmp_install//usr/lib/python3.8/site-packages:$PYTHONPATH" /usr/bin/py.test || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
