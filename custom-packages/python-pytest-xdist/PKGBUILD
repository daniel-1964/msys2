# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-xdist
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=3.6.1
pkgrel=1
pkgdesc="py.test xdist plugin for distributed testing and loop-on-failing modes (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: python-pytest-xdist'
  'pypi: pytest-xdist'
)
msys2_repository_url='https://github.com/pytest-dev/pytest-xdist/'
url='https://pytest-xdist.readthedocs.io/'
license=('spdx:MIT')
depends=("python"
         "python-pytest"
         "python-execnet")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-setuptools-scm"
             "python-wheel")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('d8a26b8dbfa97f6f5bee904816b0feb15f3b95eec1e0fcee601535a572a03f5a')

prepare() {
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MSYSTEM_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
