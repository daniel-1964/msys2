# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-xdist
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.31.0
pkgrel=1
pkgdesc="py.test xdist plugin for distributed testing and loop-on-failing modes (msys)"
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=("python"
         "python-pytest-forked"
         "python-execnet")
makedepends=("python-pytest"
             "python-filelock"
             "python-setuptools"
             "python-setuptools-scm")
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/pytest-dev/pytest-xdist/archive/v${pkgver}.tar.gz")
sha512sums=('48c18802f5573ed4ceb061e82aea2046e3b19ad54e595ef666e095409d393cc301e729edd34eab999eae85184fc22c9d5fcda6ae334d183265e4baddcb6dfa46')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  # Hack entry points by installing it
  local OLD_PYTHON_PATH=$PYTHONPATH

  msg "Python test for msys"
  cd "${srcdir}/python-build"
    /usr/bin/python setup.py install --root="$PWD/tmp_install" --optimize=1 --skip-build

  _PATHONPATH1="${srcdir}/python-build/tmp_install/usr/lib/python3.8/site-packages"
  _PATHONPATH2="${OLD_PYTHON_PATH};${srcdir}/python-build/tests"
  PYTHONPATH="${_PATHONPATH1};${_PATHONPATH2}" py.test || true

}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
