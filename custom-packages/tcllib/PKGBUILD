# $Id$
# Maintainer: Oscar Fuentes <ofv@wanadoo.es>
# Based on Arch's PKGBUILD

pkgbase=tcllib
pkgname="${pkgbase}"
pkgver=1.20
pkgrel=1
pkgdesc="Set of pure-Tcl extensions (msys)"
arch=('any')
url="https://core.tcl.tk/tcllib/"
license=('bsd')
depends=("tcl")
source=(https://github.com/tcltk/tcllib/archive/tcllib-${pkgver/./-}.tar.gz)
sha256sums=('e56892698903e0cc902a75a0eb9324845efcd3257c41e09edec25a6ab1654cf3')

package(){
  cd ${srcdir}/tcllib-tcllib-${pkgver/./-}
  tclsh installer.tcl \
    -pkg-path ${pkgdir}/usr/lib/tcllib${pkgver} \
    -app-path ${pkgdir}/usr/bin \
    -nroff-path ${pkgdir}/usr/share/man/mann \
    -no-examples \
    -no-html \
    -no-wait \
    -no-gui
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgbase}/
  cp license.terms ${pkgdir}/usr/share/licenses/${pkgbase}/COPYING
  rm -f $pkgdir/usr/share/man/mann/{try,string,variable,zlib,coroutine}.n
}
