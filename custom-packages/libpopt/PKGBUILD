# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# include patches from Archlinux AUR mingw-w64 stuff

pkgbase=popt
pkgname=('libpopt' 'libpopt-devel')
pkgver=1.16
pkgrel=1
pkgdesc="C library for parsing command line parameters (msys)"
arch=('any')
url="http://www.rpm5.org/"
license=("MIT")
makedepends=('gcc'
             'pkg-config'
             'gettext-devel'
             'doxygen')
depends=('gettext')
options=('staticlibs' 'strip')
source=(#"http://rpm5.org/files/popt/popt-${pkgver}.tar.gz"
        https://web.archive.org/web/20170922140539/rpm5.org/files/popt/popt-${pkgver}.tar.gz
        '0001-nl_langinfo.mingw32.patch'
        '197416.all.patch'
        '217602.all.patch'
        '278402-manpage.all.patch'
        '318833.all.patch'
        '356669.all.patch'
        '367153-manpage.all.patch'
        'get-w32-console-maxcols.mingw32.patch'
        'no-uid-stuff-on.mingw32.patch'
        'http://pkgs.fedoraproject.org/cgit/rpms/popt.git/plain/popt-1.16-pkgconfig.patch'
        'http://pkgs.fedoraproject.org/cgit/rpms/popt.git/plain/popt-1.16-execfail.patch'
        'http://pkgs.fedoraproject.org/cgit/rpms/popt.git/plain/popt-1.16-help.patch'
        '1.16-cygwin.patch'
        '1.16-vpath.patch')
sha256sums=('d6a01d72cf18dbe20d173fed7bc32cf87daafe62cf6b87672f6b434daff1ed74'
            '9d0d7a257f72cf6fa70c34417ed0e7c0949c3abaa2906cca07ad37c48af306a6'
            '38ea3b1e9b49129c666bdb58afeaa7648336148eaaec96fb52850edb5b351d7e'
            '6554fae42ea6117747df66879872e59746ff8f44130f678c9b99fa23580107c7'
            '99b9b5547edfac75eddb90083e3d19c0076d25f1d3e1ad165b49af81ecad8ce9'
            '6a2d7d78a391041e8fecd8b8c952ac4a4d5368ff006ec37b21551ee23dd99362'
            '3acd9334d967ea84ef75da2fdcb8b9efc4301b9755e1773fd6a4488c7b77ef61'
            'eb885494ee5b9e8e904c1235b38bac28f248d236728cb28f22f99aee93c46989'
            '1b9904854730e8b06d6bbb6e9475f33185bf485dbb63dda64d92962ce7715894'
            'c2b45f5750c0acd118fddab75f7a50e16cc8b8dc3cb4240992eeceb36fa83ad2'
            '024f15c24307a0bf019fe54cf7082193f28e673cfa32525253beaf2d816b0074'
            'be51320961fb167ececb599fdea4d2972d434276b1afe039f33beee5745037d6'
            '3c3d74e073b618654281a1796343523666f0d7c6b7c5bb79ada4a588159f7402'
            '7bca4ff3551b8e1e085bd2449d12c45b34f842c0b25f7dc91dfb18cd55df7cbd'
            '2cbc06a525a77f76e61ccbebe96d9d1e710abe1d2580a7ebded494d891542ccf')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]
    then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "$srcdir/${pkgbase}-$pkgver"
  rm -rf po/ca.po || true
  apply_patch_with_msg 0001-nl_langinfo.mingw32.patch \
                       197416.all.patch \
                       217602.all.patch \
                       278402-manpage.all.patch \
                       318833.all.patch \
                       356669.all.patch \
                       367153-manpage.all.patch \
                       get-w32-console-maxcols.mingw32.patch \
                       no-uid-stuff-on.mingw32.patch \
                       popt-1.16-pkgconfig.patch \
                       popt-1.16-execfail.patch \
                       popt-1.16-help.patch
  patch -Np2 -i $srcdir/1.16-cygwin.patch
  patch -Np2 -i $srcdir/1.16-vpath.patch
  autoreconf -fiv
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  [[ -d ${srcdir}/build ]] && rm -fr ${srcdir}/build
  cp -a ${srcdir}/${pkgbase}-${pkgver} "${srcdir}/build"
  cd "${srcdir}/build"
  ./configure \
    --prefix=/usr \
    --build=x86_64-pc-msys \
    --enable-shared \
    --enable-static
  make
  [[ -d ${srcdir}/dest ]] && rm -fr ${srcdir}/dest
  make DESTDIR="${srcdir}/dest" install
  install -Dpm644 "${srcdir}/${pkgbase}-${pkgver}/COPYING" "$srcdir/dest/usr/share/licenses/${pkgbase}/LICENSE"
  find "${srcdir}/dest/usr" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}

package_libpopt() {
  groups=('libraries')
  depends=('gcc-libs' 'libintl' 'libiconv')
  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -a ${srcdir}/dest/usr/bin/msys-popt-0.dll ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share/locale  ${pkgdir}/usr/share/
}

package_libpopt-devel() {
  pkgdesc="Libpopt headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libpopt=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib/pkgconfig,share/}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/share/licenses ${pkgdir}/usr/share/
  cp -a ${srcdir}/dest/usr/share/man ${pkgdir}/usr/share/
}
