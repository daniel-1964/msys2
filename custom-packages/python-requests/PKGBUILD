# Contributor: Alethea Rose <alethea@alethearose.com>

pkgbase=requests
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=2.23.0
pkgrel=1
pkgdesc="Requests is the only Non-GMO HTTP library for Python, safe for human consumption (msys)"
arch=('any')
license=('Apache')
url="https://2.python-requests.org"
depends=("python-urllib3"
         "python-chardet"
         "python-idna")
makedepends=("python-setuptools")
checkdepends=("python-pytest-httpbin"
              "python-pytest-mock"
              "python-pysocks")
optdepends=("python-pysocks: SOCKS proxy support")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/kennethreitz/requests/archive/v${pkgver}.tar.gz")
sha256sums=('bf063ded6c5c3f238f89189a791df4a22270d1ccd88b1a21687ecb6a9c6b1335')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${CARCH} | true
  cp -a "${pkgbase}-${pkgver}" "python-build"
}

build() {
  msg "Python build for ${CARCH}"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" -O1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${pkgbase}/LICENSE"
}
