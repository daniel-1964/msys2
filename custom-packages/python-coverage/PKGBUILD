# Contributor: Frederic Wang <fred.wang@free.fr>

_realname=coverage
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=7.6.0
pkgrel=1
pkgdesc="Code coverage measurement for Python (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: coverage'
)
url='https://github.com/nedbat/coveragepy'
license=('spdx:Apache-2.0')
depends=("python")
makedepends=("python-build"
             "python-installer"
             "python-setuptools"
             "python-wheel"
             "python-devel")
options=(!strip)
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('289cc803fa1dc901f84701ac10c9ee873619320f2f9aff38794db4a4a0268d51')

prepare() { 
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 "LICENSE.txt" "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
