# $Id$
# Maintainer: Richard Leys <leys.richard@gmail.com>
# Based on Arch's PKGBUILD

pkgbase=tcl-nsf
pkgname="${pkgbase}"
pkgver=2.3.0
pkgrel=1
pkgdesc="Next Scripting Language (msys)"
arch=('i686' 'x86_64')
url="https://next-scripting.org"
license=('MIT')
depends=("tcl")
makedepends=("autotools")
source=("https://downloads.sourceforge.net/project/next-scripting/${pkgver}/nsf${pkgver}.tar.gz"
        'tcl.m4')
sha256sums=('3940c4c00e18900abac8d57c195498f563c3cdb65157257af57060185cfd7ba9'
            '2162a7f2a58cc5d31de1a6e3c47907b2bdf5b12aa44d9a4787ddd01c7745a60c')

prepare() {
  cd $srcdir/nsf${pkgver}
  sed -i '1i #define MAX(a,b) (((a)>(b))?(a):(b))' generic/nsf.c
  cp -f ${srcdir}/tcl.m4 m4/tcl.m4
  autoreconf -fiv
  cd library/mongodb
  cp -f ${srcdir}/tcl.m4 m4/tcl.m4
  autoreconf -fiv
}

build() {

  cd $srcdir/nsf${pkgver}
  ./configure --prefix=/usr --enable-64bit
  make
}

package() {
  cd $srcdir/nsf${pkgver}
  make DESTDIR="${pkgdir}" install
}
