# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=filelock
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=3.0.12
pkgrel=1
pkgdesc="A platform independent file lock (msys)"
arch=('any')
url='https://github.com/benediktschmitt/py-filelock'
license=('custom:Unlicense')
depends=("python")
makedepends=("python-setuptools")
options=('staticlibs' 'strip' '!debug')
checkdepends=("python-pytest")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/benediktschmitt/py-filelock/archive/v${pkgver}.tar.gz")
sha512sums=('0f30b54b9de2540f023855effcc582b18fedcee6681f21e1733f1b60dbd39eb148e3830bc798a44c5e8c22e7ea8ca26b6d4547fae79dacc78c62b819a3d84683')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "py-${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  /usr/bin/pytest test.py
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
