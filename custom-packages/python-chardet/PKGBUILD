# Maintainer : J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=chardet
pkgname=("python-chardet")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgdesc="Python module for character encoding auto-detection (msys)"
pkgver=3.0.4
pkgrel=1
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
makedepends=("python-setuptools")
checkdepends=('python-pytest' 'python-packaging')
#More comprehensive than PyPi source
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/chardet/chardet/archive/${pkgver}.tar.gz")
sha256sums=('d5620025cfca430f6c2e28ddbc87c3c66a5c82fa65570ae975c92911c2190189')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${pkgbase}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python test.py
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${pkgbase}/COPYING"
  local PREFIX_WIN=$(cygpath -wm /usr)
}
