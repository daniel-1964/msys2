# Maintainer : J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=chardet
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgdesc="Python module for character encoding auto-detection (msys)"
pkgver=4.0.0
pkgrel=1
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
makedepends=("python-setuptools")
checkdepends=('python-pytest' 'python-packaging')
#More comprehensive than PyPi source
source=("${_realname}-${pkgver}.tar.gz::https://github.com/chardet/chardet/archive/${pkgver}.tar.gz")
sha256sums=('67bbe3eb02691d38831ddf2719c40fe09381bc17f15321946f6cd71dc274e530')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python test.py
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
  local PREFIX_WIN=$(cygpath -wm /usr)
}
