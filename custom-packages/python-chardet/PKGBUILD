# Maintainer : J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=chardet
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgdesc="Python module for character encoding auto-detection (msys)"
pkgver=5.1.0
pkgrel=1
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
depends=('python')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest' 'python-packaging')
#More comprehensive than PyPi source
source=("${_realname}-${pkgver}.tar.gz::https://github.com/chardet/chardet/archive/${pkgver}.tar.gz")
sha256sums=('556325114dc086fb2aaaf8ca06efda4cb9eba417095d8689ddc560da97313dd9')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  msg "Python test for msys2"
  cd "${srcdir}/python-build"
  /usr/bin/python -m pytest
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python -m installer --prefix=/usr --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
