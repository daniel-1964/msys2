# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=iso8601
pkgname=("python-${pkgbase}")
provides=("python3-${pkgbase}")
conflicts=("python3-${pkgbase}")
replaces=("python3-${pkgbase}")
pkgver=0.1.12
pkgrel=1
pkgdesc="Simple module to parse ISO 8601 dates (msys)"
arch=('any')
url="https://pyiso8601.readthedocs.io/en/latest/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
options=('staticlibs' 'strip' '!debug')
source=(https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('49c4b20e1f38aa5cf109ddcd39647ac419f928512c869dc01d5c7098eddede82')

prepare() {
  cd "${srcdir}"
  rm -rf "python-build" | true
  cp -a "${pkgbase}-${pkgver}" "python-build"
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${pkgbase}/COPYING"
}
