# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=psycopg2
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.8.5
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language (msys)"
arch=('any')
url='http://initd.org/psycopg/'
license=('LGPL3')
validpgpkeys=('8AD609956CF1899418E19A856013BD3AFCF957DE')
depends=("python"
         'libpq5')
makedepends=("python-setuptools"
             'libpq-devel')
options=('staticlibs' 'strip' '!debug')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-2-8/psycopg2-$pkgver.tar.gz{,.asc}
mingw-fix.patch)
validpgpkeys=('8AD609956CF1899418E19A856013BD3AFCF957DE')
sha256sums=('f7d46240f7a1ae1dd95aab38bd74f7428d46531f69219954266d669da60c0818'
            'SKIP'
            'bd100f943db9893d8676bdde81fb3ebd0ac4a7bcf44b8d6c77bce0c75e25374a')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  apply_patch_with_msg mingw-fix.patch

  cd ${srcdir}
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
