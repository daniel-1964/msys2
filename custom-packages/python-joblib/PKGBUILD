# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=joblib
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=0.17.0
pkgrel=1
pkgdesc="Set of tools to provide lightweight pipelining in Python (msys)"
arch=('any')
url='https://joblib.readthedocs.io/'
license=('BSD')
depends=("python")
optdepends=("python-numpy: for array manipulation"
            "python-lz4: for compressed serialization")
makedepends=("python-setuptools")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('6dd8d75808624ac60434a3c6de4623dc49e4f9f3332501b467c36b0fbfdd54c9')

prepare() {
  cd "${srcdir}"
  rm -rf "python-build" | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"

  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
