# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libmariadbclient
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=3.1.13
pkgrel=2
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=('i686' 'x86_64')
url="https://mariadb.org/"
license=('LGPL')
makedepends=('gcc'
             'cmake'
             'libcurl-devel'
             'libiconv-devel' \
             'openssl-devel'
             'zlib-devel')
depends=('gcc-libs'
         'libcurl'
         'libiconv' \
         'libopenssl'
         'zlib')
#optdepends=('curl')
options=('!strip' 'staticlibs')
source=(https://archive.mariadb.org/connector-c-3.1.13/mariadb-connector-c-3.1.13-src.tar.gz{,.asc}
        001-mingw-build.patch
        002-fix-prototype.patch
        003-gcc-fix-use_VA_ARGS.patch
        3.0.8-msys-dll.patch
        3.0.8-msys-export.patch
        3.0.8-iconv.patch)
validpgpkeys=('199369E5404BD5FC7D2FE43BCBCB082A1BB943DB') #MariaDB Package Signing Key <package-signing-key@mariadb.org>
sha256sums=('0271a5edfd64b13bca5937267474e4747d832ec62e169fc2589d2ead63746875'
            'SKIP'
            'a5ef33cae8a5a5b06c5b5686628bb8e9419f6bd2305343c63e952f4da1fa0fac'                                                                                  
            '346695ce8f10c2c426f880240ef7bbe7b8d70c6e58b0cc30483735c2b2d53261'
            'fc08055e5d63e310e2658b15f6e1f00b59f2aad2dec37560c01954fac6af4a6e'
            '7610d135a709cf173a12d571a1bd1a285e032f6037488eed4144629027761bac'
            '1a6a668e27cf4c20c8cee40caefd4177573aaa969ad3768aba356cdd7e2e07a0'
            '8041e559bf381444e4eb8ef149b65796126178839e6501a36e6f86934283f11d')

prepare() {
  cd ${srcdir}/mariadb-connector-c-${pkgver}-src
  patch -Np1 -i ${srcdir}/001-mingw-build.patch
  patch -Np1 -i ${srcdir}/002-fix-prototype.patch
  patch -Np1 -i ${srcdir}/003-gcc-fix-use_VA_ARGS.patch
  patch -Np2 -i ${srcdir}/3.0.8-msys-dll.patch
  patch -Np2 -i ${srcdir}/3.0.8-msys-export.patch
  patch -Np2 -i ${srcdir}/3.0.8-iconv.patch
}

build() {
  [[ -d ${srcdir}/build ]] && rm -rf ${srcdir}/build
  mkdir -p ${srcdir}/build && cd ${srcdir}/build
  /usr/bin/cmake \
    -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DWITH_EXTERNAL_ZLIB=ON \
    -DWITH_MYSQLCOMPAT=OFF \
    -DWITH_SSL=OPENSSL \
    -DWITH_EXTERNAL_ICONV=ON \
    ../mariadb-connector-c-${pkgver}-src
  make

  msg2 "Install to dest"
  cd "${srcdir}/build"
  [[ -d ${srcdir}/dest ]] && rm -rf ${srcdir}/dest
  make DESTDIR=${srcdir}/dest install
  cp -a "${srcdir}/dest/usr/include/mariadb" "${srcdir}/dest/usr/include/mysql"
  cp -a "${srcdir}/dest/usr/lib/libmariadb.dll.a" "${srcdir}/dest/usr/lib/libmysqlclient.dll.a"
  cp -a "${srcdir}/dest/usr/lib/libmariadb.dll.a" "${srcdir}/dest/usr/lib/libmysqlclient_r.dll.a"
  cp -a "${srcdir}/dest/usr/lib/libmariadbclient.a" "${srcdir}/dest/usr/lib/libmysqlclient.a"
  cp -a "${srcdir}/dest/usr/lib/libmariadbclient.a" "${srcdir}/dest/usr/lib/libmysqlclient_r.a"
}

package_libmariadbclient() {
  groups=('libraries')
  depends=('gcc-libs' 'libopenssl' 'libiconv' 'libcurl' 'zlib')
  mkdir -p ${pkgdir}/usr/{bin,lib}
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/lib/mariadb ${pkgdir}/usr/lib/
}

package_libmariadbclient-devel() {
  pkgdesc="Libmariadbclient headers and libraries"
  groups=('development')
  depends=('libopenssl-devel' 'libcurl-devel')
  options=('staticlibs')
  depends=("${pkgbase}=${pkgver}")
  mkdir -p ${pkgdir}/usr/{bin,include,lib}
  cp -a ${srcdir}/dest/usr/bin/*config* ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib/*.a ${pkgdir}/usr/lib
  cp -a ${srcdir}/dest/usr/lib/pkgconfig ${pkgdir}/usr/lib
}
