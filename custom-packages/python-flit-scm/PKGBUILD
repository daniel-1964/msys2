# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=flit-scm
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=1.7.0
pkgrel=3
pkgdesc="A PEP 518 build backend that uses setuptools_scm to generate a version file from your version control system, then flit to build the package. (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: flit-scm'
)
url='https://gitlab.com/WillDaSilva/flit_scm'
license=('MIT')
depends=("python"
         "python-flit-core"
         "python-setuptools-scm")
makedepends=("python-build" "python-installer")
checkdepends=("python-pytest")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('961bd6fb24f31bba75333c234145fff88e6de0a90fc0f7e5e7c79deca69f6bb2')

build() {
  cp -r "${_realname/-/_}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MSYSTEM_PREFIX}/bin/python -m pytest || warning "Tests failed"
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
