# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=virtualenv
pkgbase=python-${_realname}
pkgname=("python-virtualenv")
pkgver=20.26.3
pkgrel=1
pkgdesc="Virtual Python Environment builder (msys)"
arch=('any')
msys2_references=(
  'archlinux: python-virtualenv'
  'pypi: virtualenv'
  "cpe: cpe:/a:python:virtualenv"
)
msys2_repository_url='https://github.com/pypa/virtualenv/'
url="https://virtualenv.pypa.io/"
license=('spdx:MIT')
depends=("python-platformdirs"
         "python-distlib"
         "python-filelock")
makedepends=("python-build"
             "python-installer"
             "python-hatchling"
             "python-hatch-vcs")
options=('!strip')
source=(https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz)
sha256sums=('4c43a2a236279d9ea36a0d76f98d84bd6ca94ac4e0f4a3b9d46d05e10fea542a')

build() {
  cp -r "${_realname}-${pkgver}" "python-build" && cd "python-build"
  ${MSYSTEM_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MSYSTEM_PREFIX}/bin/python -m installer --prefix=${MSYSTEM_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE -t "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/${_realname}"
}
