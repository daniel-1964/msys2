# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cryptography
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=3.2.1
pkgrel=1
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers (msys)"
url='https://pypi.org/project/cryptography/'
license=('Apache')
arch=('any')
depends=('python-cffi'
         'python-pyasn1'
         'python-idna'
         'python-asn1crypto')
makedepends=('python-setuptools'
             'python-six')
checkdepends=('python-pytest-runner'
              'python-iso8601'
              'python-pretend'
              #'python-cryptography-vectors'
              'python-hypothesis'
              'python-pytz')
source=(https://pypi.io/packages/source/c/cryptography/${_realname}-${pkgver}.tar.gz)
sha256sums=('d3d5e10be0cf2a12214ddee45c6bd203dab435e3d83b4560c03066eda600bfe3')

prepare() {
  cd ${srcdir}
  cp -a ${_realname}-${pkgver} python-build
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd ${srcdir}/python-build
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
