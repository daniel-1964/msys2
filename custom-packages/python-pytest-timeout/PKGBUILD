# Maintainer: Sarah Ottinger

_realname=pytest-timeout
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.4.2
pkgrel=1
pkgdesc='py.test plugin to abort hanging tests (msys)'
arch=('any')
url="https://bitbucket.org/pytest-dev/pytest-timeout"
license=('MIT')
depends=("python-pytest")
source=("https://github.com/pytest-dev/pytest-timeout/archive/$pkgver/${_realname}-$pkgver.tar.gz")
sha512sums=('062ce6c832a7139874ad8f165818c2a7d9cef4a2fe2c53a6c37d4f0a060cf920204f1d2d255e4d31953c0da268e708c1d305f362cd6d0ea103bc101ffb51b098')

prepare() {
  cd "$srcdir"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
