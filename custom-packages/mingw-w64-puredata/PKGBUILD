# Maintainer: Michael Gogins <michael.gogins@gmail.com>

_realname=puredata
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgrel=2
pkgver=0.55
pkgdesc="Visual programming language for audio and visuals."
arch=('any')
url="https://puredata.info/"
license=('LGPL')
provides=("${MINGW_PACKAGE_PREFIX}-puredata")
depends=("${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-portmidi"
         "${MINGW_PACKAGE_PREFIX}-portaudio"
         "${MINGW_PACKAGE_PREFIX}-fftw")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
conflicts=("${MINGW_PACKAGE_PREFIX}-puredata")
#https://msp.puredata.info/Software/
source=("http://msp.ucsd.edu/Software/pd-${pkgver}-${pkgrel}.src.tar.gz")
sha256sums=('108297f8d1dd190df8e8bb4a4ac348792accf704f9a2052d93cba8c9b8bb5a5b')

build() {
    cd $srcdir/pd-${pkgver}-${pkgrel}
    ./autogen.sh
    ./configure \
        --prefix="${MINGW_PREFIX}" \
        --without-local-portmidi \
        --without-local-portaudio \
        --enable-portmidi \
        --enable-fftw \
        --enable-portaudio \
        --with-wish=wish
    # Somewhat a hack but it works
    sed -i 's/ -ldl//g' extra/*/GNUmakefile
    make
}

package() {
    cd "$srcdir/pd-${pkgver}-${pkgrel}"
    make install DESTDIR=${pkgdir}
    cp -p src/libpd.a ${pkgdir}/${MINGW_PREFIX}/lib
    mv ${pkgdir}/${MINGW_PREFIX}/bin/pd.{def,lib} ${pkgdir}/${MINGW_PREFIX}/lib
    mv ${pkgdir}/${MINGW_PREFIX}/bin/pd.exe ${pkgdir}/${MINGW_PREFIX}/lib/pd/bin
    mv ${pkgdir}/${MINGW_PREFIX}/bin/pd.com ${pkgdir}/${MINGW_PREFIX}/bin/pd.exe
}
