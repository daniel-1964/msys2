# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
#This value is here because some python package names have capital letters
#while there is a strong preference for lowercase letters in ArchLinux and
#MSYS packages.

_pyname=pyxdg
_realname=xdg
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=0.26
pkgrel=1
pkgdesc="Python library to access freedesktop.org standards (msys)"
arch=('any')
url='https://freedesktop.org/Software/pyxdg'
license=('LGPL')
depends=("python")
makedepends=("python-setuptools")
options=('staticlibs' 'strip' '!debug')
source=("${_pyname}-${pkgver}.tar.gz"::"https://github.com/takluyver/pyxdg/archive/rel-${pkgver}.tar.gz")
sha256sums=('2bd7f0b30bdeeaa15ed7d5651b3e266b2f40a37d3170dc37646e27deb7966dce')

build() {
  msg "Python build for msys"
  cd "${srcdir}/${_pyname}-rel-${pkgver}"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/${_pyname}-rel-${pkgver}"
  /usr/bin/python setup.py check
}

package() {
  cd "${srcdir}/${_pyname}-rel-${pkgver}"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
