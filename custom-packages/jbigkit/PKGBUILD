# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname=('jbigkit' 'libjbig2' 'libjbig-devel')
pkgver=2.1
pkgrel=4
pkgdesc="Data compression library/utilities for bi-level high-resolution images (msys)"
arch=('any')
url="https://www.cl.cam.ac.uk/~mgk25/jbigkit/"
license=(GPL2)
depends=("gcc-libs")
makedepends=("gcc")
options=('staticlibs' 'strip')
source=("https://www.cl.cam.ac.uk/~mgk25/download/${pkgname}-${pkgver}.tar.gz"
        allNewMainMakefile.all.patch
        manfiles.all.patch
        pbmtoolsMakefile.all.patch
        useLibtool.all.patch
        zzz_debug.mingw.patch)
sha256sums=('de7106b6bfaf495d6865c7dd7ac6ca1381bd12e0d81405ea81e7f2167263d932'
            'ce210309c47214599c451e680043dc827980ace647385c4ae26f3725d1054d97'
            'd5cce4c1bae575563cb892c93de349a7747435c175e81ee57d77e65079d11908'
            '7f268260d596275a1bbe803a829baf6c4a6938f9e23e861dd433622e12c2487e'
            '1f580efde162267e028f43694a945557b6a7c16f9800f51b7bb17346c27df364'
            'fe579b90d3b34e6d3209af91fcd9f84a172c2e45ca88a8b23e84f8eacd04f941')

prepare() {
  cd
  patch -d "${srcdir}/${pkgname}-${pkgver}" -Np1 -i ${srcdir}/allNewMainMakefile.all.patch
  patch -d "${srcdir}/${pkgname}-${pkgver}" -Np1 -i ${srcdir}/manfiles.all.patch
  patch -d "${srcdir}/${pkgname}-${pkgver}" -Np1 -i ${srcdir}/pbmtoolsMakefile.all.patch
  patch -d "${srcdir}/${pkgname}-${pkgver}" -Np1 -i ${srcdir}/useLibtool.all.patch
  patch -d "${srcdir}/${pkgname}-${pkgver}" -Np1 -i ${srcdir}/zzz_debug.mingw.patch
}

build() {
  [[ -d "${srcdir}/build" ]] && rm -fr "${srcdir}/build"
  cp -a ${pkgname}-${pkgver} build
  cd "${srcdir}/build"
  unset CFLAGS CPPFLAGS LDFLAGS
  make prefix=/usr
  make prefix=/usr DESTDIR="${srcdir}/dest" install
  install -Dm644 COPYING "${srcdir}/dest/usr/share/licenses/${pkgname}/COPYING"
}

package_jbigkit() {
  depends=("libjbig2=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libjbig-devel() {
  pkgdesc="Libjbig headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libjbig2=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}

package_libjbig2() {
  groups=('libraries')
  depends=('gcc-libs')
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}
