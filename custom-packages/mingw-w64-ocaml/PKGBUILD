# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.10.0
pkgrel=1
pkgdesc="An industrial strength programming language supporting functional, imperative and object-oriented styles (mingw-w64)"
arch=('any')
url='https://ocaml.org/'
license=('Q Public Licence 1.0' 'GPL2')
depends=("${MINGW_PACKAGE_PREFIX}-flexdll")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "diffutils")
source=("https://caml.inria.fr/pub/distrib/ocaml-${pkgver%.*}/${_realname}-${pkgver}.tar.gz"
        "0001-set-flexdll-path.patch"
        "0002-use-flexdll_dlopen.patch"
        "0003-testsuite.patch")
sha256sums=('58d431dde66f5750ebe9b15d5a1c4872f80d283dec23448689b0d1a498b7e4c7'
            '9f0c606ce75ffff53a94bebd6b1432eb52f50f284691bde06b1b0930c86805cd'
            '31c349f351b7f5b212717912d02b4aa51061dedadac89da96cb64a81a5169cad'
            '5da49bf4d0c5b53031e9690684ca1b5a27ffb718cba5e452c19db249cc31899b')
install=ocaml-${CARCH}.install

prepare() {
  patch -d ${srcdir}/${_realname}-${pkgver} -Np1 -i ${srcdir}/0001-set-flexdll-path.patch
  patch -d ${srcdir}/${_realname}-${pkgver} -Np1 -i ${srcdir}/0002-use-flexdll_dlopen.patch
  patch -d ${srcdir}/${_realname}-${pkgver} -Np1 -i ${srcdir}/0003-testsuite.patch
  cd ${srcdir}/${_realname}-${pkgver}
  ./autogen
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -a ${_realname}-${pkgver} build-${MINGW_CHOST}

  unset FLEXDIR
  unset OCAMLDIR
  cd ${srcdir}/build-${MINGW_CHOST}
  BFD_INCLUDE_DIR=${MINGW_PREFIX}/include/binutils \
  BFD_LIB_DIR=${MINGW_PREFIX}/lib/binutils \
  AS=as \
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --mandir=${MINGW_PREFIX}/share/man \
    --enable-shared
  make
}

check() {
  # The testsuite refuses to compile due to lack of headers such as sys/resource.h
  plain "skip"
  cd ${srcdir}/build-${MINGW_CHOST}
  make tests || true
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install DESTDIR="${pkgdir}"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
  mv ${pkgdir}${MINGW_PREFIX}/lib/ocaml/stublibs/*.dll ${pkgdir}${MINGW_PREFIX}/bin
}
