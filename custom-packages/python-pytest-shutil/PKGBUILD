# Maintainer: @naveen521kk on Github, Naveen M K <naveen521kk@gmail.com>

_pyname=pytest-shutil
_realname=pytest-shutil
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
pkgver=1.7.0
pkgrel=6
pkgdesc='A goodie-bag of unix shell and environment tools for py.test (msys)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: pytest-shutil'
)
url="https://github.com/manahl/pytest-plugins"
license=('MIT')
depends=(
  "python-six"
  "python-execnet"
  "python-contextlib2"
  "python-pytest"
  "python-path"
  "python-mock"
  "python-termcolor"
)
makedepends=(
  "python-setuptools"
  "python-setuptools-scm"
  "python-setuptools-git"
)
source=("${_pyname}-${pkgver}.tar.gz::https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('d8165261de76e7508505c341d94c02b113dc963f274543abca74dbfabd021261')

prepare() {  
  cd "$srcdir"
  sed -i 's/path.py/path/g' "${_pyname//_/-}-$pkgver/setup.py"

  rm -rf python-build | true
  cp -r "${_pyname//_/-}-$pkgver" "python-build"
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
