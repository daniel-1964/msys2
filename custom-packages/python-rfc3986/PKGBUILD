# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=rfc3986
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.4.0
pkgrel=1
pkgdesc="Validating URI References per RFC 3986 (msys)"
arch=('any')
url='https://rfc3986.readthedocs.org/'
license=('Apache')
depends=("python")
makedepends=("python-setuptools")
checkdepends=("python-pytest-runner")
source=("https://pypi.io/packages/source/r/rfc3986/rfc3986-${pkgver}.tar.gz")
sha512sums=('dbcddc09331b794eafed8c2d7b81b7b632ba5cae2741e33ad12e781bc923427a05febd2f2715d037f35769b18c8d790546dfd4bbebe6e71d35777745e446888b')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py build
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py pytest
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build
}
