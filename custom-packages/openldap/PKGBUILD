# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=openldap
pkgbase=${_realname}
pkgname=("${pkgbase}" "${pkgbase}-devel" "lib${pkgbase}")
pkgver=2.6.3
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="OpenLDAP Software is an open source implementation of the Lightweight Directory Access Protocol (only client) (msys)"
depends=("libsasl"
         "libopenssl")
makedepends=("gcc"
             "libtool"
             "groff"
             "libsasl-devel"
             "make")
options=('strip' 'staticlibs' '!makeflags' '!emptydirs')
license=("The OpenLDAP Public License")
url="https://www.openldap.org/"
source=(https://www.openldap.org/software/download/OpenLDAP/openldap-release/${pkgbase}-${pkgver}.tgz
        001-backmdb.patch
        002-mingw-build-fixes.patch
        003-symdummy-error.patch
        openldap-cygwin.patch)
sha256sums=('d2a2a1d71df3d77396b1c16ad7502e674df446e06072b0e5a4e941c3d06c0d46'
            'b462bfab9362cf5cab61d67cd24902a1800fb498a152b681ee182c713b79146c'
            'ee73beee10613f2d41a7be5e4f89dee625390e44843afe02b88e02e5188398fe'
            'd697fe51361a5129a5af3ca317031ff6f4ca5ddd247ff53043880660154a4708'
            '2bbafe4d69f5f2b4c80b80fb6bc22618b0a403f727e0bd5acbcff7938cb1fd4b')

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  cd ${_realname}-${pkgver}

  # imported from https://github.com/cfengine/buildscripts/blob/master/deps-packaging/openldap/mingw_build_fixes.patch
  apply_patch_with_msg \
    002-mingw-build-fixes.patch \
    001-backmdb.patch \
    003-symdummy-error.patch
  patch -Nbp2 -i ${srcdir}/openldap-cygwin.patch
  sed -i 's/\*cygwin\*/& | *msys*/' configure.ac
  #libtoolize --force --copy
  AUTOMAKE=/usr/bin/true autoreconf -fi
}

build() {
  [[ -d "${srcdir}/build" ]] && rm -rf "${srcdir}/build"
  mkdir -p build && cd build
  unset CPPFLAGS
  export lt_cv_deplibs_check_method='pass_all'
  ../${pkgbase}-${pkgver}/configure \
    --build=x86_64-pc-msys \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-tls=openssl \
    --with-cyrus-sasl \
    --disable-slapd \
    --enable-dynamic \
    --enable-shared
  make depend
  make

  msg2 "Install to dest"
  cd "${srcdir}/build"
  make DESTDIR=${srcdir}/dest LN_S="ln -s" install
  install -Dm644 "${srcdir}/${pkgbase}-${pkgver}/LICENSE" "${srcdir}/dest/usr/share/licenses/${pkgbase}/LICENSE"
}

package_openldap() {
  depends=("lib${pkgbase}=${pkgver}")
  mkdir -p ${pkgdir}/usr/{bin,share/{licenses,man/man{1,5}}}
  cp -a ${srcdir}/dest/etc ${pkgdir}/
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share/licenses ${pkgdir}/usr/share
  cp -a ${srcdir}/dest/usr/share/man/man1 ${pkgdir}/usr/share/man
  cp -a ${srcdir}/dest/usr/share/man/man5/ld*.5 ${pkgdir}/usr/share/man/man5
}

package_libopenldap() {
  groups=('libraries')
  depends=('gcc-libs' 'libopenssl' 'libsasl')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
}

package_openldap-devel() {
  pkgdesc="Openldap headers and libraries"
  groups=('development')
  depends=('openssl-devel' 
           'libsasl-devel'
           "lib${pkgbase}=${pkgver}")
  options=('staticlibs')
  mkdir -p ${pkgdir}/usr/{include,lib,share/man/man3}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/share/man/man3 ${pkgdir}/usr/share/man
}
