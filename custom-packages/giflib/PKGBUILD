# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=giflib
pkgbase="${_realname}"
pkgname=('giflib' 'libgif7' 'libgif-devel')
pkgver=5.2.2
pkgrel=1
pkgdesc="A library for reading and writing gif images (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://sourceforge.net/projects/giflib"
msys2_references=(
  "cpe: cpe:/a:giflib_project:giflib"
)
license=("MIT")
makedepends=("gcc"
             "xmlto")
depends=("gcc-libs")
options=('staticlibs' 'strip')
source=("https://downloads.sourceforge.net/sourceforge/giflib/${_realname}-${pkgver}.tar.gz"
        "001-msys-build.patch"
        "giflib_quantize.patch")
sha256sums=('be7ffbd057cadebe2aa144542fd90c6838c6a083b5e8a9048b8ee3b66b29d5fb'
            '158ab488bb8d536a7deffeb83c9ab8db41d300998d4888f62cd8cd945605278c'
            '29f0d5a98520cf908a11325987ca9ef2454ca8cb85ce9815b75a52956b9df28a')
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  tar -xzf ${srcdir}/${_realname}-${pkgver}.tar.gz -C ${srcdir} || true

  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/001-msys-build.patch"
  # https://src.fedoraproject.org/rpms/giflib/c/109bf038d703a471b857aba44af673be103d7079
  patch -Np1 -i "${srcdir}/giflib_quantize.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make
  make DESTDIR="${srcdir}/dest" PREFIX="${MSYSTEM_PREFIX}" install
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make -C tests
}

package_giflib() {
  depends=("libgif7=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libgif7() {
  groups=('libraries')
  depends=('gcc-libs' 'gettext')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
}

package_libgif-devel() {
  pkgdesc="Libgif headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libgif7=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -a ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -a ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
