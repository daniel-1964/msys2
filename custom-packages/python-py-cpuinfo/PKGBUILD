# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=py-cpuinfo
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=9.0.0
pkgrel=2
pkgdesc="Get CPU info with pure Python 2 & 3 (msys)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/workhorsy/py-cpuinfo'
msys2_references=(
  'pypi: py-cpuinfo'
)
license=('MIT')
depends=("python")
makedepends=("python-setuptools")
checkdepends=("python-pytest-runner")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/workhorsy/py-cpuinfo/archive/v${pkgver}.tar.gz")
sha256sums=('21dfd34d85f4d1419aad63f4945d7356025794fd1529c95644f618aac390625c')

prepare() {
  rm -rf python-build | true
  cp -r "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build"
  ${MSYSTEM_PREFIX}/bin/python setup.py pytest || warning "Tests failled"
}

package() {
  cd "${srcdir}/python-build"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MSYSTEM_PREFIX}/bin/python setup.py install --prefix=${MSYSTEM_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}${MSYSTEM_PREFIX}/share/licenses/python-${_realname}/COPYING"
}
