# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('recode' 'librecode0' 'librecode-devel')
pkgbase=recode
pkgver=3.7.6
pkgrel=2
pkgdesc="Converts files between various character sets and usages (msys2)"
arch=('any')
url="http://recode.progiciels-bpi.ca/index.html"
license=('GPL3' 'LGPL')
depends=('gettext')
makedepends=('gcc'
             'gettext-devel'
             'python')
source=("https://github.com/rrthomas/recode/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz"
        01-no-undefined.patch)
sha256sums=('46c5828ac6f5fb759ecf98b16a674188d901b84675b0dbbc0afc03c542d3a954'
            '21de31138709ec460fb684b5bf110beabf31627b8e55b4131aad67c7633e77dd')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  patch -p1 -i ${srcdir}/01-no-undefined.patch

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build" ]] && rm -rf "${srcdir}/build"
  mkdir -p ${srcdir}/build && cd ${srcdir}/build
  export lt_cv_deplibs_check_method='pass_all'
  PYTHON=python \
  ../${pkgbase}-${pkgver}/configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --without-libiconv-prefix \
    --without-libintl-prefix
  make

  # Installing
  make DESTDIR="${srcdir}/dest" install
  mkdir -p "${srcdir}/dest/usr/share/licenses/${pkgbase}"
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/COPYING     "${srcdir}/dest/usr/share/licenses/${pkgbase}/COPYING"
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/COPYING-LIB "${srcdir}/dest/usr/share/licenses/${pkgbase}/COPYING-LIB"
}

package_recode() {
  depends=("librecode0=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -a ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_librecode0() {
  groups=('libraries')
  depends=('gcc-libs' 'gettext')
  mkdir -p ${pkgdir}/usr/bin
  cp -a ${srcdir}/dest/usr/bin/msys-*.dll ${pkgdir}/usr/bin/
}

package_librecode-devel() {
  pkgdesc="Librecode headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("librecode0=${pkgver}")
  mkdir -p ${pkgdir}/usr/{include,lib}
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
