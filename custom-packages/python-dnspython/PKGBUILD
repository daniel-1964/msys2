# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=dnspython
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.16.0
pkgrel=1
pkgdesc="A DNS toolkit for Python (msys)"
arch=('any')
url='http://www.dnspython.org/'
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
options=('staticlibs' 'strip' '!debug')
validpgpkeys=('A580DEE052FEC78D8ACF383DF24B3AFC8CA2F5C7')
source=(http://www.dnspython.org/kits/${pkgver}/dnspython-${pkgver}.tar.gz{,.asc})
sha256sums=('4bf5c5c12a4478ee7860ab176659cf64c4899ee76752d826b082f8af723c5cf9'
            'SKIP')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py check
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
