# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=pandas
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.0.5
pkgrel=1
pkgdesc="Cross-section and time series data analysis toolkit (msys)"
arch=('any')
url="https://pandas.pydata.org/"
license=('BSD')
depends=("python-numpy"
         "python-pytz"
         "python-dateutil")
optdepends=("python-scipy: needed for miscellaneous statistical functions"
            "python-matplotlib: needed for plotting"
            "python-numexpr: needed for accelerating certain numerical operations (recommended)"
            "python-openpyxl: needed for reading/writing Excel file")
makedepends=("python-setuptools"
             "gcc")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/pandas-dev/${_realname}/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('69c5d920a0b2a9838e677f78f4dde506b95ea8e4d30da25859db6469ded84fa8')

prepare() {
  cp -a ${_realname}-${pkgver} ${_realname}-py-${pkgver}
}

build() {
  cd ${srcdir}/${_realname}-py-${pkgver}
  /usr/bin/python setup.py build_ext --inplace

  /usr/bin/python setup.py build
}

package() {
  cd ${_realname}-py-${pkgver}
  /usr/bin/python setup.py install --skip-build \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
