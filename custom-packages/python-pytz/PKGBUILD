# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=pytz
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2020.4
pkgrel=1
pkgdesc="Cross platform time zone library for Python (msys)"
arch=('any')
url="https://pypi.python.org/pypi/pytz"
license=("MIT")
depends=('python')
source=(https://pypi.io/packages/source/p/pytz/pytz-${pkgver}.tar.gz{,.asc})
sha256sums=('3e6b7dd2d1e0a59084bcee14a17af60c5c562cdc16d828e8eba2e683d3a7e268'
            'SKIP')
validpgpkeys=('C7ECC365AB6F255E1EB9BA1701FA998FBAC6374A')

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -r "pytz-${pkgver}" "python-build"
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check(){
  msg "Python test for msys"
  cd "${srcdir}/python-build/pytz/tests"
  /usr/bin/python test_tzinfo.py
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr --root=${pkgdir} --optimize=1
  install -D LICENSE.txt ${pkgdir}/usr/share/licenses/python-${pkgbase}/LICENSE
}
