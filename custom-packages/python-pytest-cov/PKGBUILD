# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pytest-cov
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=2.8.1
pkgrel=1
pkgdesc="py.test plugin for coverage reporting with support for both centralised and distributed testing, including subprocesses and multiprocessing (msys)"
arch=('any')
url='https://github.com/schlamar/pytest-cov'
license=('MIT')
depends=("python"
         "python-coverage"
         "python-pytest")
makedepends=("python-setuptools")
checkdepends=("python-virtualenv"
#              "python-process-tests"
#              "python-pytest-cache"
              "python-pytest-xdist"
#              "python-fields"
)
options=('staticlibs' 'strip' '!debug')
source=("${_realname}-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-cov/archive/v${pkgver}.tar.gz")
sha512sums=('63e4180a5e64579c87aada2f5f3afb5bfe4c4d3f4d39d694abebb05c56761b758cb9a5ccc96569acea595b9333412f34be6b1a0e1cea8a9b580694bfa11df303')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"
  rm -rf python-build | true
  cp -a "${_realname}-${pkgver}" "python-build"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py build
}

check() {
  msg "Python test for msys"
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py test
}

package() {
  cd "${srcdir}/python-build"
  /usr/bin/python setup.py install --prefix=/usr \
    --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSE"
}
